---
title: "Unityã®BRPã«ãŠã‘ã‚‹ã‚¹ãƒ•ã‚£ã‚¢ãƒˆãƒ¬ãƒ¼ã‚·ãƒ³ã‚°ã‚’ç”¨ã„ãŸãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ã®Tips"
emoji: "ğŸ’¬"
type: "tech" # tech: æŠ€è¡“è¨˜äº‹ / idea: ã‚¢ã‚¤ãƒ‡ã‚¢
topics: ["Unity", "BRP", "Shader", "Raymarching", "SphereTracing"]
published: false
---

ã“ã®è¨˜äº‹ã¯Unityã§ã‚¹ãƒ•ã‚£ã‚¢ãƒˆãƒ¬ãƒ¼ã‚·ãƒ³ã‚°ã«ã‚ˆã‚‹ãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã‚’æ›¸ãã¨ãã®Tipsã‚’ç´¹ä»‹ã™ã‚‹ï¼
å¯¾è±¡èª­è€…ã¨ã—ã¦ã¯[VRChatã§ã©ã†ã—ã¦ã‚‚ ãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ã‚’ã—ãŸã„ã‚ãªãŸã¸è´ˆã‚‹æœ¬ã€Ver 1.1ã€‘](https://butadiene.booth.pm/items/1829988 "VRChatã§ã©ã†ã—ã¦ã‚‚ ãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ã‚’ã—ãŸã„ã‚ãªãŸã¸è´ˆã‚‹æœ¬ã€Ver 1.1ã€‘")ã®å†…å®¹ã‚’äºˆã‚ç†è§£ã—ã¦ã„ã‚‹ã‚‚ã®ã¨ã™ã‚‹ï¼

---

## ãƒ™ãƒ¼ã‚¹ã¨ãªã‚‹ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼

ã‚µãƒ³ãƒ—ãƒ«ã‚³ãƒ¼ãƒ‰: [Assets/koturn/SphereTracingTips/01_Basic/Shaders/Basic.shader](https://github.com/koturn/SphereTracingTips/blob/main/Assets/koturn/SphereTracingTips/01_Basic/Shaders/Basic.shader "Assets/koturn/SphereTracingTips/01_Basic/Shaders/Basic.shader")

æœ¬è¨˜äº‹ã§ã¯æœ€ä½é™ã®æ©Ÿèƒ½ã‚’å®Ÿè£…ã—ãŸä¸‹è¨˜ã®ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã‚’ãƒ™ãƒ¼ã‚¹ã¨ã—ã¦å¤‰æ›´ãƒ»è§£èª¬ã‚’è¡Œã£ã¦ã„ãï¼

### é–¢æ•°åãƒ»å¤‰æ•°å

#### é ‚ç‚¹ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã®å‡ºåŠ›ãƒ»ãƒ•ãƒ©ã‚°ãƒ¡ãƒ³ãƒˆã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã®å…¥åŠ›æ§‹é€ ä½“

`SV_POSITION` ã«å¯¾å¿œã™ã‚‹ãƒ¡ãƒ³ãƒã®åå‰ã¯ `pos` ã«ã—ã¦ã‚ã‚‹ï¼


### ãƒ¯ãƒ¼ãƒ«ãƒ‰ç©ºé–“/ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆç©ºé–“ã¯åˆ‡ã‚Šæ›¿ãˆå¯èƒ½

ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆç©ºé–“ã‹ãƒ¯ãƒ¼ãƒ«ãƒ‰ç©ºé–“ã®ã©ã¡ã‚‰ã§ãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ã‚’è¡Œã†ã‹ã©ã†ã‹ã¯ï¼Œã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã‚­ãƒ¼ãƒ¯ãƒ¼ãƒ‰ `_CALCSPACE_OBJECT` ã¾ãŸã¯ `_CALCSPACE_WORLD` ã«ã‚ˆã£ã¦åˆ‡ã‚Šæ›¿ãˆå¯èƒ½ã¨ã—ãŸï¼

ã¾ãŸï¼Œã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆç©ºé–“ã«ãŠã‘ã‚‹ãƒ¬ã‚¤ã®è¦–ç‚¹ã§ã‚ã‚‹ã‚«ãƒ¡ãƒ©åº§æ¨™ã¯ï¼Œé ‚ç‚¹ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã§äº‹å‰ã«è¨ˆç®—ï¼ˆãƒ¯ãƒ¼ãƒ«ãƒ‰ç©ºé–“ã«ãŠã‘ã‚‹ã‚«ãƒ¡ãƒ©åº§æ¨™ã‚’ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆç©ºé–“åº§æ¨™ã«å¤‰æ›ã™ã‚‹è¡Œåˆ—æ¼”ç®—ï¼‰ã™ã‚‹ã“ã¨ã«ã‚ˆã‚Šï¼Œå¤šå°‘ã®å‡¦ç†è² è·è»½æ¸›ã‚’è©¦ã¿ã¦ã„ã‚‹ï¼
ã‚‚ã£ã¨æœ€é©åŒ–ã™ã‚‹ãªã‚‰ãƒ¬ã‚¤ã®éæ­£è¦åŒ–æ–¹å‘ãƒ™ã‚¯ãƒˆãƒ«ã‚’æ±‚ã‚ã‚‹ã¨ã“ã‚ã¾ã§é ‚ç‚¹ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã§ã§ãã‚‹ãŒï¼Œãƒ•ãƒ©ã‚°ãƒ¡ãƒ³ãƒˆã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã§ãŸã‹ã ãŒ1å›ã®æ¸›ç®—å‘½ä»¤ã‚’å‰Šæ¸›ã™ã‚‹ã ã‘ã®çµæœã¨ã—ã‹ãªã‚‰ãªã„ãŸã‚ï¼Œå¯¾å¿œã®ãƒ¢ãƒãƒ™ãƒ¼ã‚·ãƒ§ãƒ³ä½ã„ï¼

ãƒ©ã‚¹ã‚¿ãƒ©ã‚¤ã‚ºã§ã¯3é ‚ç‚¹ã®æƒ…å ±ã®ç·šå½¢è£œé–“ãŒè¡Œã‚ã‚Œã‚‹ãŒï¼Œæ­£è¦åŒ–å‡¦ç†ã¯éç·šå½¢å¤‰æ›ãªã®ã§ï¼Œä¸‹è¨˜2ã¤ã¯ç•°ãªã‚‹çµæœã¨ãªã‚‹ï¼

- æ­£è¦åŒ–æ–¹å‘ãƒ™ã‚¯ãƒˆãƒ«ã‚’ç·šå½¢è£œé–“ã—ãŸçµæœ
- éæ­£è¦åŒ–æ–¹å‘ãƒ™ã‚¯ãƒˆãƒ«ã‚’ç·šå½¢è£œé–“ã—ãŸçµæœã‚’æ­£è¦åŒ–ã—ãŸãƒ™ã‚¯ãƒˆãƒ«

ãã®ãŸã‚ï¼Œé ‚ç‚¹ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã§æ­£è¦åŒ–æ–¹å‘ãƒ™ã‚¯ãƒˆãƒ«ã®ç®—å‡ºã¯ã§ããªã„ï¼
ã™ãªã‚ã¡ï¼Œãƒ•ãƒ©ã‚°ãƒ¡ãƒ³ãƒˆã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã‹ã‚‰æ–¹å‘ãƒ™ã‚¯ãƒˆãƒ«ã®æ­£è¦åŒ–å‡¦ç†ã‚’é™¤å»ã™ã‚‹ã“ã¨ã¯ã§ããªã„ï¼

### ã‚¹ã‚±ãƒ¼ãƒªãƒ³ã‚°

xè»¸ï¼Œyè»¸ï¼Œzè»¸ãã‚Œãã‚Œã®æ–¹å‘ã¸ã®æ‹¡å¤§ãƒ»ç¸®å°å€ç‡ã®ãƒ™ã‚¯ãƒˆãƒ« `_Scales` ã‚’è¨­ã‘ã¦ã„ã‚‹ï¼

```shaderlab
_Scales ("Scale vector", Vector) = (1.0, 1.0, 1.0, 1.0)
```

```hlsl
//! Scale vector.
uniform float3 _Scales;
```

ç©ºé–“è‡ªä½“ã®ä¼¸ç¸®ã§ã‚ã‚‹ãŸã‚ï¼Œ `map()` ã¸ã®å¼•æ•°è‡ªä½“ã« `_Scales` ã®é€†æ•°ã‚’ä¹—ç®—ã—ã¦ã„ã‚‹ï¼

ã—ã‹ã—ï¼Œãã‚Œã ã‘ã§ã¯ä¸ååˆ†ã§ï¼Œãƒ¬ã‚¤ã®é€²è¡Œé•·ã«å¯¾ã—ã¦ã®æ‹¡å¤§ãƒ»ç¸®å°å€ç‡ã®æ§é™¤ãŒå¿…è¦ã¨ãªã‚‹ï¼
ãƒ¬ã‚¤ã®æ–¹å‘ãƒ™ã‚¯ãƒˆãƒ« `rayDir` ã«å¯¾ã—ã¦ `Scales` ã®é€†æ•°ã‚’ä¹—ç®—ã—ãŸã‚‚ã®ã®é•·ã•ã®é€†æ•°ã‚’ï¼Œ1å›ã®ã‚¹ãƒ†ãƒƒãƒ—ã®ãƒ¬ã‚¤ã®é€²è¡Œé•· `d` ã«ä¹—ç®—ã™ã‚‹ã“ã¨ã§ã“ã®æ§é™¤ãŒå¯èƒ½ã¨ãªã‚‹ï¼

é•·ã•ã®é€†æ•°ã‚’ãã®ã¾ã¾ã‚³ãƒ¼ãƒ‰ã«è½ã¨ã—è¾¼ã‚€ã¨ `1.0 / length(rayDir * rcpScales)` ã¨ãªã‚‹ã®ã ãŒï¼Œã“ã‚Œã¯ [`sqrt` å‘½ä»¤](thttps://learn.microsoft.com/en-us/windows/win32/direct3dhlsl/sqrt--sm4---asm- "sqrt (sm4 - asm) - Win32 apps | Microsoft Learn")ã¨ [`div` å‘½ä»¤](https://learn.microsoft.com/en-us/windows/win32/direct3dhlsl/div--sm4---asm- "div (sm4 - asm) - Win32 apps | Microsoft Learn")ã«ãªã£ã¦ã—ã¾ã†ï¼
ä¸–ã®ä¸­ã«ã¯[é«˜é€Ÿã«é€†å¹³æ–¹æ ¹ã‚’è¨ˆç®—ã™ã‚‹ã‚¢ãƒ«ã‚´ãƒªã‚ºãƒ ](https://lipoyang.hatenablog.com/entry/2021/02/06/194619 "é«˜é€Ÿé€†å¹³æ–¹æ ¹ (fast inverse square root) ã®ã‚¢ãƒ«ã‚´ãƒªã‚ºãƒ è§£èª¬ - æ»´äº†åºµæ—¥éŒ²")ãŒã‚ã‚Šï¼ŒDirectX11çš„ã«ã‚‚ [`rsq`](https://learn.microsoft.com/en-us/windows/win32/direct3dhlsl/rsq--sm4---asm- "rsq (sm4 - asm) - Win32 apps | Microsoft Learn") ã¨ã„ã†å˜ä¸€ã®å‘½ä»¤ãŒã‚ã‚‹ï¼
ãƒãƒ¼ãƒ‰ã‚¦ã‚§ã‚¢çš„ã«ã‚‚ã‚µãƒãƒ¼ãƒˆã•ã‚Œã¦ã„ã‚‹ã¨è€ƒãˆã‚‰ã‚Œã‚‹ã®ã§ï¼Œé€†å¹³æ–¹æ ¹ã®ç®—å‡ºã«ã¯çµ„ã¿è¾¼ã¿é–¢æ•° [`rsqrt()`](https://learn.microsoft.com/en-us/windows/win32/direct3dhlsl/dx-graphics-hlsl-rsqrt "rsqrt - Win32 apps | Microsoft Learn") ã‚’ç”¨ã„ã‚‹æ–¹ãŒã‚ˆã„ï¼

ã“ã‚Œã‚‰ã‚’è¸ã¾ãˆã‚‹ã¨ï¼Œãƒãƒ¼ãƒãƒ³ã‚°ãƒ«ãƒ¼ãƒ—éƒ¨åˆ†ã®ã‚³ãƒ¼ãƒ‰ã¯ä¸‹è¨˜ã®ã‚ˆã†ã«ãªã‚‹ï¼

```hlsl
const float3 rcpScales = rcp(_Scales);
const float marchingFactor = _MarchingFactor * rsqrt(dot(rayDir * rcpScales, rayDir * rcpScales));

float rayLength = 0.0;
float d = asfloat(0x7f800000);  // +inf
for (int rayStep = 0; d >= _MinRayLength && rayLength < _MaxRayLength && rayStep < _MaxLoop; rayStep++) {
    d = map((rayOrigin + rayDir * rayLength) * rcpScales);
    rayLength += d * marchingFactor;
}

if (d >= _MinRayLength) {
    discard;
}
```

### Tetrahedron techniqueã«ã‚ˆã‚‹æ³•ç·šç®—å‡º

æ³•ç·šã®å°å‡ºã¯[Tetrahedron technique](https://iquilezles.org/articles/normalsSDF/ "Inigo Quilez :: computer graphics, mathematics, shaders, fractals, demoscene and more")ã‚’ç”¨ã„ã¦ï¼Œè·é›¢é–¢æ•°ã®è©•ä¾¡ãŒ4å›ã§æ¸ˆã‚€ã‚ˆã†ã«ã—ãŸï¼
å…ƒè¨˜äº‹ã®å°å‡ºéç¨‹ã¯ã‚„ã‚„é£›ã°ã—æ°—å‘³ã ãŒï¼Œã‚‚ã†å°‘ã—ä¸å¯§ã«æ›¸ãã¨ä¸‹è¨˜ã®ã‚ˆã†ã«ãªã‚‹ï¼ˆæŠ˜ã‚Šç•³ã¿éƒ¨åˆ†ï¼‰ï¼

:::details Tetrahedron techniqueã®å°å‡ºéç¨‹
ã¾ãšï¼Œé™°é–¢æ•° $\boldsymbol{f}$ ï¼ˆ $f: \mathbb{R}^3 \mapsto \mathbb{R}$ ï¼‰ã®ç‚¹ $\boldsymbol{p}$ ã«ãŠã‘ã‚‹æ­£è¦åŒ–æ³•ç·šãƒ™ã‚¯ãƒˆãƒ« $\boldsymbol{n}$ ã¯ä¸‹è¨˜ã®ã¨ãŠã‚Šï¼

$$
\boldsymbol{n} = normalize(\nabla f( \boldsymbol{p} ))
= normalize \begin{pmatrix}
\dfrac{\partial f(\boldsymbol{p})}{\partial x}
\\
\dfrac{\partial f(\boldsymbol{p})}{\partial y}
\\
\dfrac{\partial f(\boldsymbol{p})}{\partial z}
\end{pmatrix}  \\
$$

ã“ã“ã§ä¸‹è¨˜ã®4ã¤ã®ãƒ™ã‚¯ãƒˆãƒ«ã‚’è¨­ã‘ã‚‹ï¼

$$
\begin{cases}
\boldsymbol{k}_0 & = & \begin{pmatrix}1 & -1 & -1\end{pmatrix}^T  \\
\boldsymbol{k}_1 & = & \begin{pmatrix}-1 & -1 & 1\end{pmatrix}^T  \\
\boldsymbol{k}_2 & = & \begin{pmatrix}-1 & 1 & -1\end{pmatrix}^T  \\
\boldsymbol{k}_3 & = & \begin{pmatrix}1 & 1 & 1\end{pmatrix}^T
\end{cases}
$$

ã“ã®4ã¤ã®ãƒ™ã‚¯ãƒˆãƒ«ã‚’ç”¨ã„ã‚‹ã¨ä¸‹è¨˜ã®ã‚ˆã†ã«æ‰“ã¡æ¶ˆã—ãŒç™ºç”Ÿã—ï¼Œã‚¼ãƒ­ãƒ™ã‚¯ãƒˆãƒ«ã¨ãªã‚‹ï¼

$$
\sum_i \boldsymbol{k}_i f(\boldsymbol{p}) =
\begin{pmatrix}
f(\boldsymbol{p})_x  \\
-f(\boldsymbol{p})_y  \\
-f(\boldsymbol{p})_z
\end{pmatrix} +
\begin{pmatrix}
-f(\boldsymbol{p})_x  \\
f(\boldsymbol{p})_y  \\
-f(\boldsymbol{p})_z
\end{pmatrix} +
\begin{pmatrix}
-f(\boldsymbol{p})_x  \\
-f(\boldsymbol{p})_y  \\
f(\boldsymbol{p})_z
\end{pmatrix} +
\begin{pmatrix}
f(\boldsymbol{p})_x  \\
f(\boldsymbol{p})_y  \\
f(\boldsymbol{p})_z
\end{pmatrix}
= \boldsymbol{0}
$$

å¾“ã£ã¦ï¼Œ $h$ ã‚’å¾®å°ãªå€¤ã¨ã™ã‚‹ã¨ï¼Œ

$$
\begin{align}
\boldsymbol{m} &= \sum_i \boldsymbol{k}_i \dfrac{f(\boldsymbol{p} + h \boldsymbol{k}_i)}{h}  \\
&= \sum_i \boldsymbol{k}_i \dfrac{f(\boldsymbol{p} + h \boldsymbol{k}_i)}{h} - \boldsymbol{0} \\
&= \sum_i \boldsymbol{k}_i \dfrac{f(\boldsymbol{p} + h \boldsymbol{k}_i)}{h} - \sum_i \boldsymbol{k}_i \dfrac{f(\boldsymbol{p})}{h} \\
&= \sum_i \left( \boldsymbol{k}_i \dfrac{f(\boldsymbol{p} + h \boldsymbol{k}_i)}{h} - \boldsymbol{k}_i \dfrac{f(\boldsymbol{p})}{h} \right) \\
&= \sum_i \boldsymbol{k}_i \dfrac{f(\boldsymbol{p} + h \boldsymbol{k}_i) - f(\boldsymbol{p})}{h}  \\
&= \sum_i \boldsymbol{k}_i \nabla_{\boldsymbol{k}_i} f(\boldsymbol{p})  \\
&= \sum_i \boldsymbol{k}_i (\boldsymbol{k}_i \cdot \nabla f(\boldsymbol{p}))
\end{align}
$$

ã¨ãªã‚‹ï¼

ã“ã“ã§ï¼Œ $\boldsymbol{m}$ ã®æˆåˆ† $x$ ã ã‘ã«æ³¨ç›®ã™ã‚‹ã¨ï¼Œä»¥ä¸‹ã‚’å¾—ã‚‹ï¼

$$
\begin{align}
m_x &= \sum_i k_{ix} (\boldsymbol{k}_i \cdot \nabla f(\boldsymbol{p}))  \\
&= \sum_i \left( (k_{ix} \boldsymbol{k}_i) \cdot \nabla f(\boldsymbol{p}) \right)  \\
&= \nabla f(\boldsymbol{p}) \cdot \sum_i k_{ix} \boldsymbol{k}_i  \\
&= \nabla f(\boldsymbol{p}) \cdot \begin{pmatrix} 4 & 0 & 0 \end{pmatrix}^T  \\
&= 4 \dfrac{\partial f(\boldsymbol{p})}{\partial x}
\end{align}
$$

$y$, $z$ ã«é–¢ã—ã¦ã‚‚åŒæ§˜ã§ã‚ã‚‹ãŸã‚ï¼Œæœ€çµ‚çš„ã« $\boldsymbol{m}$ ã¯ä¸‹è¨˜ã®ã‚ˆã†ã«ãªã‚‹ï¼

$$
\begin{align}
\boldsymbol{m} &= \begin{pmatrix}
m_x & m_y & m_z
\end{pmatrix}^T  \\
&= \begin{pmatrix}
4 \dfrac{\partial f(\boldsymbol{p})}{\partial x} & 4 \dfrac{\partial f(\boldsymbol{p})}{\partial y} & 4 \dfrac{\partial f(\boldsymbol{p})}{\partial z}
\end{pmatrix}^T  \\
&= 4 \begin{pmatrix}
\dfrac{\partial f(\boldsymbol{p})}{\partial x} & \dfrac{\partial f(\boldsymbol{p})}{\partial y} & \dfrac{\partial f(\boldsymbol{p})}{\partial z}
\end{pmatrix}^T  \\
&= 4 \nabla f(\boldsymbol{p})
\end{align}
$$

æ­£è¦åŒ–ã‚’è¡Œã†ãªã‚‰å®šæ•°å€ã¯ç„¡è¦–ã§ãã‚‹ã®ã§ï¼Œæ­£è¦åŒ–æ³•ç·šãƒ™ã‚¯ãƒˆãƒ« $\boldsymbol{n}$ ã¯

$$
\begin{align}
\boldsymbol{n}
&= normalize \left( \nabla f(\boldsymbol{p}) \right)  \\
&= normalize \left( \dfrac{1}{4} \boldsymbol{m} \right)  \\
&= normalize \left( \sum_i \boldsymbol{k}_i \dfrac{f(\boldsymbol{p} + h \boldsymbol{k}_i)}{4h} \right) \\
&= normalize \left( \sum_i \boldsymbol{k}_i f(\boldsymbol{p} + h \boldsymbol{k}_i) \right)
\end{align}
$$

ã¨ãªã‚‹ï¼
:::

ãƒ—ãƒ­ã‚°ãƒ©ãƒ ã«è½ã¨ã—è¾¼ã‚€ã¨ä¸‹è¨˜ã®ã‚ˆã†ã«ãªã‚‹ï¼

```hlsl
/*!
 * @brief Calculate normal of the objects with tetrahedron technique.
 * @param [in] p  Position of the tip of the ray.
 * @return Normal of the objects.
 * @see https://iquilezles.org/articles/normalsSDF/
 */
float3 calcNormal(float3 p)
{
    static const float2 k = float2(1.0, -1.0);
    static const float3 ks[] = {k.xyy, k.yxy, k.yyx, k.xxx};
    static const float h = 0.0001;

    const float3 rcpScales = rcp(_Scales);
    float3 normal = float3(0.0, 0.0, 0.0);

    for (int i = 0; i < 4; i++) {
        normal += ks[i] * map((p + ks[i] * h) * rcpScales);
    }

    return normalize(normal);
}
```

å¤šãã®ä¾‹ã ã¨ãƒ«ãƒ¼ãƒ—ã‚’ç”¨ã„ãšã«æ›¸ã‹ã‚Œã¦ã„ã‚‹ãŒï¼Œã‚ãˆã¦ãƒ«ãƒ¼ãƒ—ã®å½¢ã¨ã—ãŸã®ã¯ä¸‹è¨˜2ç‚¹ã®ç†ç”±ã«ã‚ˆã‚‹ï¼

- ãƒ«ãƒ¼ãƒ—ãŒè¡Œã‚ã‚Œã‚‹ã‚³ãƒ¼ãƒ‰ã®æ–¹ãŒã‚³ãƒ¼ãƒ‰ã‚µã‚¤ã‚ºã¯å°ã•ãï¼Œã‚³ãƒ³ãƒ‘ã‚¤ãƒ«æ™‚é–“ã‚‚çŸ­ã„
- æ‰‹å‹•ã§ã‚¢ãƒ³ãƒ«ãƒ¼ãƒ—ã•ã‚Œã¦ã„ã‚‹ã‚³ãƒ¼ãƒ‰ã‹ã‚‰ãƒ«ãƒ¼ãƒ—ã™ã‚‹ã‚³ãƒ¼ãƒ‰ã¸ã®ã‚³ãƒ³ãƒ‘ã‚¤ãƒ«ã¯ã§ããªã„ãŒï¼Œãƒ«ãƒ¼ãƒ—ã‚’ç”¨ã„ãŸã‚³ãƒ¼ãƒ‰ã‹ã‚‰ã‚¢ãƒ³ãƒ­ãƒ¼ãƒ«ã—ãŸã‚³ãƒ¼ãƒ‰ã¸ã®ã‚³ãƒ³ãƒ‘ã‚¤ãƒ«ã¯ `[unroll]` ã®æŒ‡å®šã«ã‚ˆã‚Šå®¹æ˜“ã«ã§ãã‚‹

ã‚¢ãƒ³ãƒ­ãƒ¼ãƒ«ã«é–¢ã—ã¦ï¼Œä¾‹ãˆã°ä¸‹è¨˜2ã¤ã®ã©ã¡ã‚‰ã‚‚åŒã˜ã‚³ãƒ¼ãƒ‰ç”ŸæˆãŒã•ã‚Œã‚‹ï¼

```hlsl
/*!
 * @brief Calculate normal of the objects with tetrahedron technique.
 * @param [in] p  Position of the tip of the ray.
 * @return Normal of the objects.
 * @see https://iquilezles.org/articles/normalsSDF/
 */
float3 calcNormal(float3 p)
{
    static const float h = 0.0001;  // replace by an appropriate value
    static const float2 k = float2(1.0, -1.0);
    static const float2 ks = h * k;

    const float3 rcpScales = rcp(_Scales);

    return normalize(
        k.xyy * map((p + ks.xyy) * rcpScales)
            + k.yxy * map((p + ks.yxy) * rcpScales)
            + k.yyx * map((p + ks.yyx) * rcpScales)
            + map((p + ks.xxx) * rcpScales).xxx);
}
```

```hlsl
/*!
 * @brief Calculate normal of the objects with tetrahedron technique.
 * @param [in] p  Position of the tip of the ray.
 * @return Normal of the objects.
 * @see https://iquilezles.org/articles/normalsSDF/
 */
float3 calcNormal(float3 p)
{
    static const float2 k = float2(1.0, -1.0);
    static const float3 ks[] = {k.xyy, k.yxy, k.yyx, k.xxx};
    static const float h = 0.0001;

    const float3 rcpScales = rcp(_Scales);
    float3 normal = float3(0.0, 0.0, 0.0);

    [unroll]  // <- unroll lopp!!
    for (int i = 0; i < 4; i++) {
        normal += ks[i] * map((p + ks[i] * h) * rcpScales);
    }

    return normalize(normal);
}
```

ã‚¢ãƒ³ãƒ­ãƒ¼ãƒ«ã®æŒ‡å®šãŒãªã„å ´åˆï¼Œãƒ«ãƒ¼ãƒ—å†…ã®ã‚³ãƒ¼ãƒ‰ãŒååˆ†ã«å°ã•ã‘ã‚Œã°ã‚¢ãƒ³ãƒ­ãƒ¼ãƒ«ã•ã‚Œã‚‹ï¼
ä¾‹ãˆã°ï¼Œ `map()` ãŒå˜ç´”ãªçƒã®è·é›¢é–¢æ•°ã§ã‚ã‚Œã°ï¼Œç„¡æŒ‡å®šã®foræ–‡ã§ã‚ã£ã¦ã‚‚ã‚¢ãƒ³ãƒ­ãƒ¼ãƒ«ã•ã‚ŒãŸçµæœã¨ãªã‚‹ï¼

### pragma target 3.0 ã¨ç’°å¢ƒå…‰

[VRChatã§ã©ã†ã—ã¦ã‚‚ ãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ã‚’ã—ãŸã„ã‚ãªãŸã¸è´ˆã‚‹æœ¬ã€Ver 1.1ã€‘](https://butadiene.booth.pm/items/1829988 "VRChatã§ã©ã†ã—ã¦ã‚‚ ãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ã‚’ã—ãŸã„ã‚ãªãŸã¸è´ˆã‚‹æœ¬ã€Ver 1.1ã€‘")ã§ã¯pragma targetã®æŒ‡å®šã¯ãªã‹ã£ãŸãŒï¼Œæœ¬è¨˜äº‹ã®ã™ã¹ã¦ã®ã‚³ãƒ¼ãƒ‰ã§ `#pragma target 3.0` ã‚’æŒ‡å®šã—ã¦ã„ã‚‹ï¼
ã“ã‚Œã¯[æŒ‡å®šãŒãªã‘ã‚Œã° `#pragma target 2.5` ãŒæŒ‡å®šã•ã‚Œã¦ã„ã‚‹ã®ã¨åŒæ§˜ã«ãªã‚‹](https://docs.unity3d.com/2022.3/Documentation/Manual/SL-ShaderCompileTargets.html "Unity - Manual: Targeting shader models and GPU features in HLSL")ãŒï¼Œã“ã‚Œã ã¨ç’°å¢ƒå…‰ã‚’é ‚ç‚¹å˜ä½ã§è¡Œã†è¨­å®šã§ã‚ã‚‹ãŸã‚ï¼Œ `ShaderSHPerPixel()` ãŒä½•ã‚‚è¡Œã‚ãªããªã‚Šï¼Œãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ã§ã®ãƒ©ã‚¤ãƒ†ã‚£ãƒ³ã‚°ã«ä¸éƒ½åˆãªãŸã‚ã§ã‚ã‚‹ï¼

ä¸‹è¨˜ã¯Half-Lambertã¨Blinn-Phongã«ã‚ˆã‚‹ãƒ©ã‚¤ãƒ†ã‚£ãƒ³ã‚°é–¢æ•°ã ãŒï¼Œ `ShadeSHPerPixel()` ãŒæ©Ÿèƒ½ã—ãªã‘ã‚Œã°æç”»çµæœãŒå¤§ããç•°ãªã‚‹ï¼

```hlsl
/*!
 * Calculate lighting.
 * @param [in] color  Base color.
 * @param [in] worldPos  World coordinate.
 * @param [in] worldNormal  Normal in world space.
 * @param [in] atten  Light attenuation.
 * @param [in] ambient  Ambient light.
 * @return Lighting applied color.
 */
half4 calcLighting(half4 color, float3 worldPos, float3 worldNormal, half atten, half3 ambient)
{
    const float3 worldViewDir = normalize(_WorldSpaceCameraPos - worldPos);
#if defined(USING_LIGHT_MULTI_COMPILE) && defined(USING_DIRECTIONAL_LIGHT)
    const float3 worldLightDir = UnityWorldSpaceLightDir(worldPos);
#else
    const float3 worldLightDir = normalize(UnityWorldSpaceLightDir(worldPos));
#endif  // defined(USING_LIGHT_MULTI_COMPILE) && defined(USING_DIRECTIONAL_LIGHT)
    const fixed3 lightCol = _LightColor0.rgb * atten;

    // Lambertian reflectance.
    const float nDotL = dot(worldNormal, worldLightDir);
    const half3 diffuse = lightCol * pow(nDotL * 0.5 + 0.5, 2.0);  // will be mul instruction.

    // Specular reflection.
    const float nDotH = dot(worldNormal, normalize(worldLightDir + worldViewDir));
    const half3 specular = pow(max(0.0, nDotH), _SpecPower) * _SpecColor.rgb * lightCol;

    // Ambient color.
#if UNITY_SHOULD_SAMPLE_SH
    ambient = ShadeSHPerPixel(worldNormal, ambient, worldPos);
#endif  // UNITY_SHOULD_SAMPLE_SH

    const half4 outColor = half4((diffuse + ambient) * color.rgb + specular, color.a);

    return outColor;
}
```

#### Directional Lightã‚ã‚ŠçŠ¶æ³ä¸‹ã§ã®ç’°å¢ƒå…‰ã®æœ‰ç„¡æ¯”è¼ƒ

ç’°å¢ƒå…‰ã®å‡¦ç†ãŒã‚ã‚Œã°ï¼Œå…¨ä½“çš„ã«æ˜ã‚‹ã‚ã«ãªã‚‹ï¼

| ç’°å¢ƒå…‰ã‚ã‚Š | ç’°å¢ƒå…‰ãªã— |
|:-:|:-:|
| [![ç’°å¢ƒå…‰ã‚ã‚Š](/images/koturn-unity-sphere-tracing-tips/AmbientWithDirectionalLight.png)](/images/koturn-unity-sphere-tracing-tips/AmbientWithDirectionalLight.png "ç’°å¢ƒå…‰ã‚ã‚Š") | [![ç’°å¢ƒå…‰ãªã—](/images/koturn-unity-sphere-tracing-tips/NoAmbientWithDirectionalLight.png)](/images/koturn-unity-sphere-tracing-tips/AmbientWithDirectionalLight.png "ç’°å¢ƒå…‰ãªã—") |

#### Directional Lightãªã—çŠ¶æ³ä¸‹ã§ã®ç’°å¢ƒå…‰ã®æœ‰ç„¡æ¯”è¼ƒ

ç’°å¢ƒå…‰ã®å‡¦ç†ãŒã‚ã‚Œã°ï¼ŒDirectional LightãŒå­˜åœ¨ã—ãªã„çŠ¶æ³ä¸‹ã§ã‚‚æ˜ã‚‹ã•ã‚’ç¢ºä¿ã§ãã‚‹ï¼
ä¸€æ–¹ã§ï¼Œç’°å¢ƒå…‰ã®å‡¦ç†ãŒãªã‘ã‚Œã°çœŸã£é»’ãªæç”»çµæœã¨ãªã£ã¦ã—ã¾ã†ï¼

VRChatã«ã¯Directional LightãŒå­˜åœ¨ã—ãªã„ãƒ¯ãƒ¼ãƒ«ãƒ‰ã‚‚ã‚ã‚‹ãŸã‚ï¼ŒDirectional LightãŒå­˜åœ¨ã—ãªã„çŠ¶æ³ã¯è€ƒæ…®ã™ã¹ãã§ã‚ã‚‹ï¼

| ç’°å¢ƒå…‰ã‚ã‚Š | ç’°å¢ƒå…‰ãªã— |
|:-:|:-:|
| [![ç’°å¢ƒå…‰ã‚ã‚Š](/images/koturn-unity-sphere-tracing-tips/AmbientWithoutDirectionalLight.png)](/images/koturn-unity-sphere-tracing-tips/AmbientWithoutDirectionalLight.png "ç’°å¢ƒå…‰ã‚ã‚Š") | [![ç’°å¢ƒå…‰ãªã—](/images/koturn-unity-sphere-tracing-tips/NoAmbientWithoutDirectionalLight.png)](/images/koturn-unity-sphere-tracing-tips/AmbientWithoutDirectionalLight.png "ç’°å¢ƒå…‰ãªã—") |

### SPS-I

ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã®SPS-Iã¸ã®å¯¾å¿œã¯ [lilxyzw/Shader-MEMO](https://github.com/lilxyzw/Shader-MEMO/blob/main/Assets/SPSITest.shader "Shader-MEMO/Assets/SPSITest.shader at main Â· lilxyzw/Shader-MEMO") ã‚’å‚è€ƒã«è¡Œã£ãŸï¼
ç«¯çš„ã«ã¯ä¸‹è¨˜ã®éƒ¨åˆ†ã®æ§‹é€ ä½“ã¸ã®ãƒ¡ãƒ³ãƒè¿½åŠ ã¨è»¢é€ï¼ŒåˆæœŸåŒ–å‡¦ç†ãŒè©²å½“ã—ï¼Œã“ã‚ŒãŒã‚ã‚‹ã ã‘ã§SPS-Iã«å¯¾å¿œã—ãŸã“ã¨ã«ãªã‚‹ï¼
è©³ç´°ã¯ãã‚Œãã‚Œã®ãƒã‚¯ãƒ­ã®å®šç¾©ã‚’å‚ç…§ã™ã‚‹ã“ã¨ï¼

```hlsl
        struct appdata
        {
            /* ---------- ç•¥ ---------- */

            // Instance ID for single pass instanced rendering, `uint instanceID : SV_InstanceID`.
            UNITY_VERTEX_INPUT_INSTANCE_ID
        };

        /* ---------- ç•¥ ---------- */

        struct v2f
        {
            /* ---------- ç•¥ ---------- */

            // Instance ID for single pass instanced rendering, `uint instanceID : SV_InstanceID`.
            UNITY_VERTEX_INPUT_INSTANCE_ID
            // Stereo target eye index for single pass instanced rendering, `stereoTargetEyeIndex` and `stereoTargetEyeIndexSV`.
            UNITY_VERTEX_OUTPUT_STEREO
        }

        v2f vert(appdata v)
        {
            /* ---------- ç•¥ ---------- */

            UNITY_SETUP_INSTANCE_ID(v);
            UNITY_TRANSFER_INSTANCE_ID(v, o);
            UNITY_INITIALIZE_VERTEX_OUTPUT_STEREO(o);

            /* ---------- ç•¥ ---------- */
        }

        /* ---------- ç•¥ ---------- */

        fout frag(v2f fi)
        {
            UNITY_SETUP_INSTANCE_ID(fi);
            UNITY_SETUP_STEREO_EYE_INDEX_POST_VERTEX(fi);

            /* ---------- ç•¥ ---------- */
        }
```


### Tagsã®è¨­å®š

Tagsã¯ä¸‹è¨˜ã®æŒ‡å®šã‚’è¡Œã£ã¦ã„ã‚‹ï¼ãã‚Œãã‚Œã®è§£èª¬ã‚’è¡Œã†ï¼

```shaderlab
Tags
{
    "Queue" = "AlphaTest"
    // "RenderType" = "Transparent"
    "DisableBatching" = "True"
    "IgnoreProjector" = "True"
    "VRCFallback" = "Hidden"
}
```

#### Queue

æœ¬è¨˜äº‹ã§ã¯é€šå¸¸ã®ã‚¹ãƒ•ã‚£ã‚¢ãƒˆãƒ¬ãƒ¼ã‚·ãƒ³ã‚°ã«ã‚ˆã‚‹ä¸é€æ˜ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã®æç”»ã‚’è¡Œã†ã“ã¨ã‚’å‰æã¨ã—ã¦ã„ã‚‹ï¼
`discard` ã‚’è¡Œã†ãŸã‚ï¼Œ `"Queue" = "AlphaTest"` (2450) ã®æŒ‡å®šãŒå¦¥å½“ã¨è€ƒãˆã¦ã„ã‚‹ï¼

#### RenderType

Post Processing ã® Ambient Occlusion ã« Scalable Ambient Obscurance (SAO) ã‚’ç”¨ã„ã¦ã„ã‚‹å ´åˆï¼Œ[ä¸‹æ•·ãã«ã—ã¦ã„ã‚‹ãƒ¡ãƒƒã‚·ãƒ¥ã«æ²¿ã£ã¦èª¤ã£ãŸå½±ãŒæç”»](https://tips.hecomi.com/entry/2019/01/27/233137#%E3%83%95%E3%82%A9%E3%83%AF%E3%83%BC%E3%83%89%E3%81%A7-DepthNormalTexture-%E3%82%92%E4%BD%BF%E3%81%86%E5%A0%B4%E5%90%88 "Unity ã§è·é›¢é–¢æ•°ã®è¨˜è¿°ã ã‘ã§ãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ãŒã§ãã‚‹ uRaymarching ã‚’ Forward / XR å¯¾å¿œã—ãŸ - å‡¹ã¿Tips")ã•ã‚Œã‚‹ï¼
Multi Scale Volumetric Obscurance (MSVO) ã§ã‚ã‚Œã°å•é¡Œã¯ç™ºç”Ÿã—ãªã„ã®ã ãŒï¼ŒVRChatã®ã‚ˆã†ãªãƒ—ãƒ©ãƒƒãƒˆãƒ•ã‚©ãƒ¼ãƒ ã«ãŠã‘ã‚‹ã‚¢ãƒã‚¿ãƒ¼ã«ãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ã®ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã‚’ä»•è¾¼ã‚€å ´åˆï¼ŒSAOãŒæ¡ç”¨ã•ã‚Œã¦ã„ã‚‹ãƒ¯ãƒ¼ãƒ«ãƒ‰ã«é­é‡ã™ã‚‹å¯èƒ½æ€§ãŒã‚ã‚‹ï¼

ã“ã®å•é¡Œã‚’å›é¿ã™ã‚‹ã«ã¯ï¼Œä¸‹è¨˜2ã¤ã®ã©ã¡ã‚‰ã‹ã®æ‰‹æ®µã‚’å–ã‚‹ã¨ã‚ˆã„ï¼

- RenderTypeã‚¿ã‚°ã‚’è¨˜è¿°ã—ãªã„
- `"RenderType" = "Transparent"`

#### DisableBatching

åŒã˜ãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã®ãƒãƒ†ãƒªã‚¢ãƒ«ã‚’æŒã¤ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆãŒ2ã¤ä»¥ä¸Šã‚ã‚‹å ´åˆï¼Œãƒãƒƒãƒãƒ³ã‚°ãŒè¡Œã‚ã‚Œï¼Œãƒ­ãƒ¼ã‚«ãƒ«åº§æ¨™ãŒå–å¾—ã§ããªããªã‚‹ç¾è±¡ãŒç™ºç”Ÿã™ã‚‹ï¼
ã“ã‚Œã‚’é˜²ããŸã‚ã«ï¼Œ `"DisableBatching" = "True"` ã‚’æŒ‡å®šã™ã‚‹ï¼

ãŸã ã—ï¼ŒGPU Instancingã«å¯¾å¿œã•ã›ã¦ã„ã‚‹å ´åˆï¼Œã“ã®æŒ‡å®šã¯ä¸è¦ã§ã‚ã‚‹ï¼
SPS-Iå¯¾å¿œã‚’è¡Œã£ã¦ã„ã‚‹ã“ã¨ã«ã‚ˆã‚Šï¼ŒGPUã‚¤ãƒ³ã‚¹ã‚¿ãƒ³ã‚·ãƒ³ã‚°ã«ã‚‚å¯¾å¿œã—ãŸã‚³ãƒ¼ãƒ‰ã¨ãªã£ã¦ã„ã‚‹ã®ã§ï¼Œ `#pragma multi_compile_instancing` ã‚’è¨˜è¼‰ã—ï¼Œã‚¤ãƒ³ã‚¹ãƒšã‚¯ã‚¿ã§æœ‰åŠ¹ã«ã™ã‚Œã°ï¼Œãƒãƒƒãƒãƒ³ã‚°ã®å•é¡Œã¯è§£æ¶ˆã§ãã‚‹ï¼

#### IgnoreProjector

ãƒ—ãƒ­ã‚¸ã‚§ã‚¯ã‚¿ã§ã®æŠ•å½±ã¯ãƒãƒ†ãƒªã‚¢ãƒ«ã®å·®ã—æ›¿ãˆã«ã‚ˆã£ã¦è¡Œã‚ã‚Œã‚‹ï¼
ã—ã‹ã—ï¼Œãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ã¯åŸºæœ¬çš„ã«ãƒ¡ãƒƒã‚·ãƒ¥ã‚’ç„¡è¦–ã—ã¦æç”»ã™ã‚‹ã‚‚ã®ã§ã‚ã‚‹ãŸã‚ï¼Œå¯¾ç­–ãªã—ã ã¨ä¸‹æ•·ãã«ã—ã¦ã„ã‚‹ãƒ¡ãƒƒã‚·ãƒ¥ã®å½¢çŠ¶ã«æ²¿ã£ã¦æŠ•å½±ãŒè¡Œã‚ã‚Œã¦ã—ã¾ã†ï¼
ãƒ—ãƒ­ã‚¸ã‚§ã‚¯ã‚¿ã®æŠ•å½±ã‚’ç„¡åŠ¹ã«ã™ã‚‹ãŸã‚ã« `"IgnoreProjector" = "True"` ã‚’æŒ‡å®šã™ã‚‹ã“ã¨ã‚’ã‚ªã‚¹ã‚¹ãƒ¡ã™ã‚‹ï¼

#### VRCFallback

ãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ã¯åŸºæœ¬çš„ã«ãƒ¡ãƒƒã‚·ãƒ¥ã«æ²¿ã‚ãªã„æç”»ã‚’è¡Œã†ã‚‚ã®ãªã®ã§ï¼Œãƒ•ã‚©ãƒ¼ãƒ«ãƒãƒƒã‚¯å¯èƒ½ãªã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã¯ãªã„ï¼
ãã®ãŸã‚ï¼Œ `"VRCFallback" = "Hidden"` ã‚’æŒ‡å®šã—ï¼Œã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ãƒ–ãƒ­ãƒƒã‚¯ã•ã‚Œã¦ã„ã‚‹å ´åˆã¯éè¡¨ç¤ºã¨ãªã‚‹ã‚ˆã†ã«è¨­å®šã—ã¦ãŠãã“ã¨ã‚’ã‚ªã‚¹ã‚¹ãƒ¡ã™ã‚‹ï¼

### ãƒ•ãƒ©ã‚°ãƒ¡ãƒ³ãƒˆã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã§ã®æ·±åº¦å‡ºåŠ›

`SV_Depth` ã‚»ãƒãƒ³ãƒ†ã‚£ã‚¯ã‚¹ã«ã‚ˆã‚Šãƒ•ãƒ©ã‚°ãƒ¡ãƒ³ãƒˆã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã§ã®æ·±åº¦å‡ºåŠ›ãŒå¯èƒ½ã¨ãªã‚‹ãŒï¼Œã“ã‚Œã¯ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã‚­ãƒ¼ãƒ¯ãƒ¼ãƒ‰ã«ã‚ˆã‚Šåˆ‡ã‚Šæ›¿ãˆå¯èƒ½ã¨ã—ãŸï¼

```shaderlab
[Toggle(_SVDEPTH_ON)]
_SVDepth ("SV_Depth ouput", Int) = 1
```

```hlsl
#pragma shader_feature_local_fragment _ _SVDEPTH_ON
```

ã¾ãŸï¼Œ[VRChatã§ã©ã†ã—ã¦ã‚‚ ãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ã‚’ã—ãŸã„ã‚ãªãŸã¸è´ˆã‚‹æœ¬ã€Ver 1.1ã€‘](https://butadiene.booth.pm/items/1829988 "VRChatã§ã©ã†ã—ã¦ã‚‚ ãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ã‚’ã—ãŸã„ã‚ãªãŸã¸è´ˆã‚‹æœ¬ã€Ver 1.1ã€‘")ã®ã‚³ãƒ¼ãƒ‰ä¸­ã«ã¯è¨˜è¼‰ãŒãªã„ãŒï¼ŒOpenGLç³»ã ã¨ã‚¯ãƒªãƒƒãƒ”ãƒ³ã‚°åº§æ¨™ã®æ·±åº¦å€¤ãŒDirectXç³»ã¨ç•°ãªã£ã¦ãŠã‚Šï¼Œ `SV_Depth` ã¨ã—ã¦å‡ºåŠ›ã™ã‚‹æ·±åº¦å€¤ã¨ã—ã¦ 0.0ï½1.0 ã®ç¯„å›²ã¸å¤‰æ›ãŒå¿…è¦ã¨ãªã‚‹ï¼

| ç¨®åˆ¥ | Near | Far |
|-|-|-|
| DirectX | 1.0 | 0.0 |
| OpenGL | -1.0 | 1.0 |

ã‚³ãƒ¼ãƒ‰ä¸­ã®ä¸‹è¨˜ã®é–¢æ•°ãŒè©²å½“ã™ã‚‹ï¼

```hlsl
/*!
 * @brief Get depth from clip space position.
 * @param [in] clipPos  Clip space position.
 * @return Depth value.
 */
float getDepth(float4 clipPos)
{
    const float depth = clipPos.z / clipPos.w;
    UNITY_REVERSE_Z
#if defined(SHADER_API_GLCORE) \
    || defined(SHADER_API_OPENGL) \
    || defined(SHADER_API_GLES) \
    || defined(SHADER_API_GLES3)
    // [-1.0, 1.0] -> [0.0, 1.0]
    // Near: -1.0
    // Far: 1.0
    return depth * 0.5 + 0.5;
#else
    // [0.0, 1.0] -> [0.0, 1.0] (No conversion)
    // Near: 1.0
    // Far: 0.0
    return depth;
#endif
}
```

ã“ã®åˆ¤å®šã«ã¯ `HLSLSuport.cginc` ã§å®šç¾©ã•ã‚Œã¦ã„ã‚‹ `UNITY_REVERSED_Z` ã‚’ç”¨ã„ã¦ã‚‚ã‚ˆã„ï¼

```hlsl
/*!
 * @brief Get depth from clip space position.
 * @param [in] clipPos  Clip space position.
 * @return Depth value.
 */
float getDepth(float4 clipPos)
{
    const float depth = clipPos.z / clipPos.w;
#if defined(UNITY_REVERSED_Z)
    // [0.0, 1.0] -> [0.0, 1.0] (No conversion)
    // Near: 1.0
    // Far: 0.0
    return depth;
#else
    // [-1.0, 1.0] -> [0.0, 1.0]
    // Near: -1.0
    // Far: -1.0
    return depth * 0.5 + 0.5;
#endif  // defined(UNITY_REVERSED_Z)
}
```

---

## æŠ•å½±å…ˆã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆ

### Quad

Unityã®ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã®Quadã§ã‚‚å•é¡Œã¯ãªã„ï¼
ãŸã ã—ï¼ŒUVï¼Œæ³•ç·šï¼Œæ¥å¹³é¢ã¯å¿…è¦ãªã„ãŸã‚ï¼Œãã‚Œã‚‰ã‚’å«ã¾ãªã„ãƒ¡ãƒƒã‚·ãƒ¥ã‚’ç”¨æ„ã—ã¦ã‚‚ã‚ˆã„ã‹ã‚‚ã—ã‚Œãªã„ï¼

### Cube

Unityã®ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã‚­ãƒ¥ãƒ¼ãƒ–ã¯UVã‚„æ³•ç·šã‚’è€ƒæ…®ã—ã¦24é ‚ç‚¹12ãƒãƒªã‚´ãƒ³ã¨ãªã£ã¦ã„ã‚‹ãŒï¼Œãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ã®æŠ•å½±ã‚’è¡Œã†Cubeã¨ã—ã¦ã¯UVï¼Œæ³•ç·šï¼Œæ¥å¹³é¢ã¯å¿…è¦ã§ã¯ãªã„ãŸã‚8é ‚ç‚¹12ãƒãƒªã‚´ãƒ³ã®ã‚­ãƒ¥ãƒ¼ãƒ–ã§ååˆ†ã§ã‚ã‚‹ã—ï¼Œé ‚ç‚¹æƒ…å ±ã«UVï¼Œæ³•ç·šï¼Œæ¥å¹³é¢ã‚’å«ã‚ãªãã¦ã‚ˆã„ï¼
ï¼ˆãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã‚­ãƒ¥ãƒ¼ãƒ–ã¯ãã‚Œãã‚Œã®é¢ã®æ³•ç·šã‚’ä¸€æ§˜ã«ã™ã‚‹ãŸã‚ã«24é ‚ç‚¹å¿…è¦ã¨ãªã£ã¦ã„ã‚‹ï¼‰

ã‚¹ãƒ•ã‚£ã‚¢ãƒˆãƒ¬ãƒ¼ã‚·ãƒ³ã‚°ã§ãƒ¬ã‚¤ã®é€²è¡Œã®åå¾©å›æ•°ãŒå¤šããªã‚‹ã®ã¯ï¼Œãƒ¬ã‚¤ãŒã‚ã‚‹ç¨‹åº¦æç”»ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã«æ¼¸è¿‘ã—ã¤ã¤ã‚‚çµå±€ã¯å¤–ã‚Œã¦ã„ã‚‹å ´åˆã§ã‚ã‚‹ï¼
Cubeã®å†…å´ã«ã®ã¿ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã‚’æç”»ã™ã‚‹ã¨ã„ã†å‰æãŒã‚ã‚‹ãªã‚‰ï¼Œæç”»ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã‚’å®Œå…¨ã«è¢«è¦†ã™ã‚‹å¿…è¦æœ€å°é™ã®ã‚µã‚¤ã‚ºã®Cubeï¼ˆç›´æ–¹ä½“ã§ã‚‚ã‚ˆã„ï¼‰ã‚’ç”¨æ„ã™ã‚‹ã¨å¤šå°‘ã®å‡¦ç†è² è·ã®è»½æ¸›ã«ã¤ãªãŒã‚‹ã¨æ€ã‚ã‚Œã‚‹ï¼

ã“ã®ç¨‹åº¦ã®ãƒ¡ãƒƒã‚·ãƒ¥ã§ã‚ã‚Œã°[Unityã®ã‚¹ã‚¯ãƒªãƒ—ãƒˆã§ä½œæˆã™ã‚‹ã“ã¨ãŒå¯èƒ½](https://github.com/koturn/SphereTracingTips/tree/main/Assets/koturn/SphereTracingTips/Editor "Assets/koturn/SphereTracingTips/Editor")ã§ã‚ã‚‹ï¼

---

## ForwardBaseä»¥å¤–ã®ãƒ¬ãƒ³ãƒ€ãƒªãƒ³ã‚°ãƒ‘ã‚¹ã¸ã®å¯¾å¿œ

ã‚µãƒ³ãƒ—ãƒ«ã‚³ãƒ¼ãƒ‰: [Assets/koturn/SphereTracingTips/02_Pass/Shaders/BaseAddShadow.shader](https://github.com/koturn/SphereTracingTips/blob/main/Assets/koturn/SphereTracingTips/02_Pass/Shaders/BaseAddShadow.shader "Assets/koturn/SphereTracingTips/02_Pass/Shaders/BaseAddShadow.shader")

BRPã«ãŠã‘ã‚‹ãƒ¬ãƒ³ãƒ€ãƒªãƒ³ã‚°ãƒ‘ã‚¹ã«ã¯Forward Baseä»¥å¤–ã«ã‚‚Forward Addã¨Shadow CasterãŒã‚ã‚‹ï¼
æœ¬ç« ã§ã¯ã“ã®2ã¤ã®ãƒ¬ãƒ³ãƒ€ãƒªãƒ³ã‚°ãƒ‘ã‚¹ã®å®Ÿè£…ã«ã¤ã„ã¦è¿°ã¹ã‚‹ï¼

### ForwardAdd Pass

Forward Addãƒ‘ã‚¹ã®å®Ÿè£…ã¯ç°¡å˜ã§ï¼ŒForward Baseã® `vert()` ã¨ `frag()` ã‚’ä½¿ã„å›ã™å½¢ã§ã‚ˆã„ï¼
Tagsã« `"LightMode" = "ForwardAdd"` ã‚’æŒ‡å®šã™ã‚‹ã®ã‚’å¿˜ã‚Œãªã„ã“ã¨ï¼
ã¾ãŸï¼Œå„ç¨®ã‚­ãƒ¼ãƒ¯ãƒ¼ãƒ‰ã¸ã®å¯¾å¿œã¯

- `#pragma multi_compile_fwdadd`
- `#pragma multi_compile_fwdadd_fullshadows`

ã®ã©ã¡ã‚‰ã‹ã‚’æŒ‡å®šã™ã‚‹å¿…è¦ãŒã‚ã‚‹ãŒï¼Œæ©Ÿèƒ½ã®å¤šã„å¾Œè€…ã‚’æŒ‡å®šã™ã‚‹ã“ã¨ã«ã—ãŸï¼

```hlsl
Pass
{
    Name "FORWARD_ADD"
    Tags
    {
        "LightMode" = "ForwardAdd"
    }

    Blend [_SrcBlend] One
    ZWrite Off

    CGPROGRAM
    // #pragma multi_compile_fwdadd
    #pragma multi_compile_fwdadd_fullshadows
    #pragma multi_compile_fog

    #pragma vertex vert
    #pragma fragment frag
    ENDCG
}
```

ForwardAddãƒ‘ã‚¹ã§ã¯ `UNITY_PASS_FORWARDADD` ãƒã‚¯ãƒ­ãŒå®šç¾©ã•ã‚Œã‚‹ãŒï¼Œ `UNITY_SHOULD_SAMPLE_SH` ã®å®šç¾©ãŒä¸‹è¨˜ã®é€šã‚Šã§ã‚ã‚‹ãŸã‚ï¼Œç’°å¢ƒå…‰ãŒForwardBaseåˆ†ã‚‚å«ã‚ã¦äºŒé‡ã«è¨ˆä¸Šã•ã‚Œã‚‹ã‚ã‘ã§ã¯ãªã„ï¼

```hlsl
#define UNITY_SHOULD_SAMPLE_SH (defined(LIGHTPROBE_SH) && !defined(UNITY_PASS_FORWARDADD) && !defined(UNITY_PASS_PREPASSBASE) && !defined(UNITY_PASS_SHADOWCASTER) && !defined(UNITY_PASS_META))
```

### ShadowCaster Pass

ShadowCasterãƒ‘ã‚¹ã«ã¤ã„ã¦ã¯[hecomiã•ã‚“ã®è¨˜äº‹](https://tips.hecomi.com/entry/2019/01/27/233137#DepthTexture-%E3%81%AE%E7%94%9F%E6%88%90 "Unity ã§è·é›¢é–¢æ•°ã®è¨˜è¿°ã ã‘ã§ãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ãŒã§ãã‚‹ uRaymarching ã‚’ Forward / XR å¯¾å¿œã—ãŸ - å‡¹ã¿Tips")ã®å†…å®¹ãã®ã¾ã¾ã§ã‚ã‚‹ï¼
ãŸã ï¼Œãƒ¬ã‚¤ã®æ­£è¦åŒ–å‰ã®æ–¹å‘ãƒ™ã‚¯ãƒˆãƒ«ã«ã¤ã„ã¦ã¯é ‚ç‚¹ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã§è¨ˆç®—å¯èƒ½ãªãŸã‚ï¼Œãƒ•ãƒ©ã‚°ãƒ¡ãƒ³ãƒˆã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã§ã¯è¨ˆç®—ã—ãªã„ã‚ˆã†ã«ã—ã¦ã„ã‚‹ï¼ˆæ¡ä»¶åˆ†å²ãŒã‚ã‚‹ï¼Œã¾ãŸï¼Œã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆç©ºé–“ã§ã®ãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ã ã¨è¡Œåˆ—æ¼”ç®—ã‚‚åŠ ã‚ã‚Šï¼Œã‚„ã‚„ã‚³ã‚¹ãƒˆé«˜ã§ã‚‚ã‚ã‚‹ãŸã‚ï¼‰ï¼

```hlsl
Pass
{
    Name "SHADOW_CASTER"
    Tags
    {
        "LightMode" = "ShadowCaster"
    }

    Blend Off
    ZWrite On

    CGPROGRAM
    #pragma multi_compile_shadowcaster

    #pragma vertex vertShadowCaster
    #pragma fragment fragShadowCaster


    /*!
     * @brief Input of the vertex shader, vertShadowCaster().
     * @see vertShadowCaster
     */
    struct appdata_shadowcaster
    {
        //! Object space position of the vertex.
        float4 vertex : POSITION;
        // instanceID for single pass instanced rendering.
        UNITY_VERTEX_INPUT_INSTANCE_ID
    };

    /*!
     * @brief Output of the vertex shader, vertShadowCaster()
     * and input of fragment shader, fragShadowCaster().
     * @see vertShadowCaster
     * @see fragShadowCaster
     */
    struct v2f_shadowcaster
    {
        // V2F_SHADOW_CASTER;
        // `float3 vec : TEXCOORD0;` is unnecessary even if `!defined(SHADOWS_CUBE) || defined(SHADOWS_CUBE_IN_DEPTH_TEX)`
        // because calculate `vec` in fragment shader.

        //! Clip space position of the vertex.
        float4 pos : SV_POSITION;
        //! Ray origin in object/world space
        float3 rayOrigin : TEXCOORD0;
        //! Unnormalized ray direction in object/world space.
        float3 rayDirVec : TEXCOORD1;
        // instanceID for single pass instanced rendering.
        UNITY_VERTEX_INPUT_INSTANCE_ID
        // stereoTargetEyeIndex for single pass instanced rendering.
        UNITY_VERTEX_OUTPUT_STEREO
    };

    #if defined(SHADER_API_GLCORE) || defined(SHADER_API_GLES) || defined(SHADER_API_D3D9)
    typedef fixed face_t;
    #    define FACE_SEMANTICS VFACE
    #else
    typedef bool face_t;
    #    define FACE_SEMANTICS SV_IsFrontFace
    #endif  // defined(SHADER_API_GLCORE) || defined(SHADER_API_GLES) || defined(SHADER_API_D3D9)


    float3 getCameraDirVec(float4 screenPos);
    bool isFacing(face_t facing);


    /*!
     * @brief Vertex shader function for ShadowCaster pass.
     * @param [in] v  Input data.
     * @return Interpolation source data for fragment shader function, fragShadowCaster().
     * @see fragShadowCaster
     */
    v2f_shadowcaster vertShadowCaster(appdata_shadowcaster v)
    {
        v2f_shadowcaster o;
        UNITY_INITIALIZE_OUTPUT(v2f_shadowcaster, o);

        UNITY_SETUP_INSTANCE_ID(v);
        UNITY_TRANSFER_INSTANCE_ID(v, o);
        UNITY_INITIALIZE_VERTEX_OUTPUT_STEREO(o);

        //
        // TRANSFER_SHADOW_CASTER(o)
        //
        o.pos = UnityObjectToClipPos(v.vertex);
    #if !defined(SHADOWS_CUBE) || defined(SHADOWS_CUBE_IN_DEPTH_TEX)
        o.pos = UnityApplyLinearShadowBias(o.pos);
    #endif  // !defined(SHADOWS_CUBE) || defined(SHADOWS_CUBE_IN_DEPTH_TEX)

        float4 screenPos = ComputeNonStereoScreenPos(o.pos);
        COMPUTE_EYEDEPTH(screenPos.z);

    #if defined(_CALCSPACE_WORLD)
        o.rayOrigin = mul(unity_ObjectToWorld, v.vertex).xyz;
    #    if defined(SHADOWS_CUBE) && !defined(SHADOWS_CUBE_IN_DEPTH_TEX)
        o.rayDirVec = getCameraDirVec(screenPos);
    #    else
        if (UNITY_MATRIX_P[3][3] == 1.0) {
            // For directional light.
            o.rayDirVec = -UNITY_MATRIX_V[2].xyz;
        } else if (abs(unity_LightShadowBias.x) < 1.0e-5) {
            // For depth output of camera.
            o.rayDirVec = o.rayOrigin - _WorldSpaceCameraPos.xyz;
        } else {
            // For spot light.
            o.rayDirVec = getCameraDirVec(screenPos);
        }
    #    endif
    #else
        o.rayOrigin = v.vertex.xyz;
    #    if defined(SHADOWS_CUBE) && !defined(SHADOWS_CUBE_IN_DEPTH_TEX)
        o.rayDirVec = mul((float3x3)unity_WorldToObject, getCameraDirVec(screenPos));
    #    else
        if (UNITY_MATRIX_P[3][3] == 1.0) {
            // For directional light.
            o.rayDirVec = mul((float3x3)unity_WorldToObject, -UNITY_MATRIX_V[2].xyz);
        } else if (abs(unity_LightShadowBias.x) < 1.0e-5) {
            // For depth output of camera.
            o.rayDirVec = o.rayOrigin - mul(unity_WorldToObject, float4(_WorldSpaceCameraPos, 1.0)).xyz;
        } else {
            // For spot light.
            o.rayDirVec = mul((float3x3)unity_WorldToObject, getCameraDirVec(screenPos));
        }
    #    endif
    #endif  // defined(_CALCSPACE_WORLD)

        return o;
    }

    /*!
     * @brief Fragment shader function for ShadowCaster pass.
     * @param [in] fi  Input data from vertex shader.
     * @param [in] facing  Facing parameter.
     * @return Depth of fragment.
     */
    fout fragShadowCaster(v2f_shadowcaster fi, face_t facing : FACE_SEMANTICS)
    {
        UNITY_SETUP_INSTANCE_ID(fi);
        UNITY_SETUP_STEREO_EYE_INDEX_POST_VERTEX(fi);

        const float3 rayOrigin = fi.rayOrigin;
        const float3 rayDir = normalize(isFacing(facing) ? fi.rayDirVec : -fi.rayDirVec);

        const float3 rcpScales = rcp(_Scales);
        const float marchingFactor = _MarchingFactor * rsqrt(dot(rayDir * rcpScales, rayDir * rcpScales));

        float rayLength = 0.0;
        float d = asfloat(0x7f800000);  // +inf
        for (int rayStep = 0; d >= _MinRayLength && rayLength < _MaxRayLength && rayStep < _MaxLoopShadowCaster; rayStep++) {
            d = map((rayOrigin + rayDir * rayLength) * rcpScales);
            rayLength += d * marchingFactor;
        }

        if (d >= _MinRayLength) {
            discard;
        }

    #if defined(_CALCSPACE_WORLD)
        const float3 worldFinalPos = rayOrigin + rayDir * rayLength;
    #else
        const float3 localFinalPos = rayOrigin + rayDir * rayLength;
        const float3 worldFinalPos = mul(unity_ObjectToWorld, float4(localFinalPos, 1.0)).xyz;
    #endif  // defined(_CALCSPACE_WORLD)

    #if defined(SHADOWS_CUBE) && !defined(SHADOWS_CUBE_IN_DEPTH_TEX)
        //
        // TRANSFER_SHADOW_CASTER_NORMALOFFSET
        //
        const float3 vec = worldFinalPos - _LightPositionRange.xyz;

        //
        // SHADOW_CASTER_FRAGMENT
        //
        fout fo;
        fo.color = UnityEncodeCubeShadowDepth((length(vec) + unity_LightShadowBias.x) * _LightPositionRange.w);
        return fo;
    #else
        //
        // TRANSFER_SHADOW_CASTER_NORMALOFFSET
        //
        float3 worldPos = worldFinalPos;
        if (unity_LightShadowBias.z != 0.0) {
    #    if defined(USING_LIGHT_MULTI_COMPILE) && defined(USING_DIRECTIONAL_LIGHT)
            const float3 worldLightDir = UnityWorldSpaceLightDir(worldPos);
    #    else
            const float3 worldLightDir = normalize(UnityWorldSpaceLightDir(worldPos));
    #    endif  // defined(USING_LIGHT_MULTI_COMPILE) && defined(USING_DIRECTIONAL_LIGHT)
    #    if defined(_CALCSPACE_WORLD)
            const float3 worldNormal = calcNormal(worldFinalPos);
    #    else
            const float3 worldNormal = UnityObjectToWorldNormal(calcNormal(localFinalPos));
    #    endif  // defined(_CALCSPACE_WORLD)
            const float shadowCos = dot(worldNormal, worldLightDir);
            const float shadowSine = sqrt(1.0 - shadowCos * shadowCos);
            const float normalBias = unity_LightShadowBias.z * shadowSine;
            worldPos.xyz -= worldNormal * normalBias;
        }
        const float4 clipPos = UnityApplyLinearShadowBias(UnityWorldToClipPos(worldPos));

        //
        // SHADOW_CASTER_FRAGMENT
        //
        fout fo;
        fo.color = float4(0.0, 0.0, 0.0, 0.0);
    #    if defined(_SVDEPTH_ON)
        fo.depth = getDepth(clipPos);
    #    endif  // defined(_SVDEPTH_ON)
        return fo;
    #endif  // defined(SHADOWS_CUBE) && !defined(SHADOWS_CUBE_IN_DEPTH_TEX)
    }

    /*!
     * @brief Get unnormalized camera direction vector from screen space position.
     * @param [in] Screen space position.
     * @return Camera direction in world space.
     */
    float3 getCameraDirVec(float4 screenPos)
    {
        float2 sp = (screenPos.xy / screenPos.w) * 2.0 - 1.0;

        // Following code is equivalent to: sp.x *= _ScreenParams.x / _ScreenParams.y;
        sp.x *= _ScreenParams.x * _ScreenParams.w - _ScreenParams.x;

        return UNITY_MATRIX_V[0].xyz * sp.x
            + UNITY_MATRIX_V[1].xyz * sp.y
            + -UNITY_MATRIX_V[2].xyz * abs(UNITY_MATRIX_P[1][1]);
    }

    /*!
     * @brief Identify whether surface is facing the camera or facing away from the camera.
     * @param [in] facing  Facing variable (fixed or bool).
     * @return True if surface facing the camera, otherwise false.
     */
    bool isFacing(face_t facing)
    {
    #if defined(SHADER_API_GLCORE) || defined(SHADER_API_GLES) || defined(SHADER_API_D3D9)
        return facing >= 0.0;
    #else
        return facing;
    #endif  // defined(SHADER_API_GLCORE) || defined(SHADER_API_GLES) || defined(SHADER_API_D3D9)
    }
    ENDCG
}
```

ãƒ¡ãƒƒã‚·ãƒ¥ã®è£è¡¨ã«ã‚ˆã£ã¦ãƒ¬ã‚¤ã®æ–¹å‘ã‚’åè»¢ã•ã›ãªã‘ã‚Œã°ï¼Œ `Cull Front` ï¼ˆè¡¨é¢ã®ãƒãƒªã‚´ãƒ³ã‚’ã‚«ãƒªãƒ³ã‚°ï¼‰ã‚’æŒ‡å®šã—ãŸã¨ãï¼Œä¸‹è¨˜è¡¨å·¦ã®ç”»åƒã®ã‚ˆã†ã«å½±ãŒã†ã¾ãæç”»ã•ã‚Œãªã„ï¼ˆç”»åƒä¸­ã§ã¯å°ã•ãªç‚¹3ã¤ã¨ãªã£ã¦ã„ã‚‹ï¼‰ï¼

| ãƒ¬ã‚¤æ–¹å‘åè»¢ãªã— | ãƒ¬ã‚¤æ–¹å‘åè»¢ã‚ã‚Š |
|-|-|
| [![ShadowCaster Passï¼ŒCull FrontæŒ‡å®šã§ãƒ¬ã‚¤ã‚’åè»¢ã•ã›ãªã„å ´åˆ](/images/koturn-unity-sphere-tracing-tips/ShadowCasterCullFrontNonInvertRayDirection.png)](/images/koturn-unity-sphere-tracing-tips/ShadowCasterCullFrontNonInvertRayDirection.png "ShadowCaster Passï¼ŒCull FrontæŒ‡å®šã§ãƒ¬ã‚¤ã‚’åè»¢ã•ã›ãªã„å ´åˆ") | [![ShadowCaster Passï¼ŒCull FrontæŒ‡å®šã§ãƒ¬ã‚¤ã‚’åè»¢ã•ã›ã‚‹å ´åˆ](/images/koturn-unity-sphere-tracing-tips/ShadowCasterCullFrontInvertRayDirection.png)](/images/koturn-unity-sphere-tracing-tips/ShadowCasterCullFrontInvertRayDirection.png "ShadowCaster Passï¼ŒCull FrontæŒ‡å®šã§ãƒ¬ã‚¤ã‚’åè»¢ã•ã›ã‚‹å ´åˆ") |

ã¾ãŸï¼Œãƒ¬ã‚¤ã‚’åè»¢ã•ã›ãŸã¨ã—ã¦ã‚‚ `Cull Front` ã ã¨ï¼Œä¸‹è¨˜è¡¨å³ã®ç”»åƒã®ã‚ˆã†ã«ãƒãƒªã‚´ãƒ³ãƒ¡ãƒƒã‚·ãƒ¥ã¨ã®æ¥è§¦éƒ¨åˆ†ã®å½±ã®æç”»ã«å°‘ã—å•é¡ŒãŒã‚ã‚‹ï¼

| `Cull Back` | `Cull Front` |
|-|-|
| [![ShadowCaster Passï¼ŒCull Backã§ãƒãƒªã‚´ãƒ³ãƒ¡ãƒƒã‚·ãƒ¥ã¨æ¥è§¦](/images/koturn-unity-sphere-tracing-tips/ShadowCasterCullBackIntersect.png)](/images/koturn-unity-sphere-tracing-tips/ShadowCasterCullBackIntersect.png "ShadowCaster Passï¼ŒCull Backã§ãƒãƒªã‚´ãƒ³ãƒ¡ãƒƒã‚·ãƒ¥ã¨æ¥è§¦") | [![ShadowCaster Passï¼ŒCull Frontã§ãƒãƒªã‚´ãƒ³ãƒ¡ãƒƒã‚·ãƒ¥ã¨æ¥è§¦](/images/koturn-unity-sphere-tracing-tips/ShadowCasterCullFrontIntersect.png)](/images/koturn-unity-sphere-tracing-tips/ShadowCasterCullFrontIntersect.png "ShadowCaster Passï¼ŒCull Frontã§ãƒãƒªã‚´ãƒ³ãƒ¡ãƒƒã‚·ãƒ¥ã¨æ¥è§¦") |


---

## Builtin Render Pipelineã®æ¨™æº–ãƒ©ã‚¤ãƒ–ãƒ©ãƒªã‚’ç”¨ã„ãŸãƒ©ã‚¤ãƒ†ã‚£ãƒ³ã‚°

æœ¬è¨˜äº‹ã§ãƒ™ãƒ¼ã‚¹ã«ã™ã‚‹ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã¯Lambertåå°„ãƒ¢ãƒ‡ãƒ«ã«ã‚ˆã‚‹æ‹¡æ•£åå°„ã¨Blinn-Phongåå°„ãƒ¢ãƒ‡ãƒ«ã«ã‚ˆã‚‹é¡é¢åå°„ã®ãƒ©ã‚¤ãƒ†ã‚£ãƒ³ã‚°ã‚’è‡ªå‰å®Ÿè£…ã—ã¦ã„ãŸï¼
ã—ã‹ã—ï¼Œãƒ©ã‚¤ãƒ†ã‚£ãƒ³ã‚°ã®å®Ÿè£…ã¯é¢å€’ã§ã‚ã‚Šï¼Œä½•ã¨ã‹ã—ã¦æ¥½ã‚’ã—ãŸã„ã‚‚ã®ã§ã‚ã‚‹ï¼
ã“ã®ç« ã§ã¯BRPã®æ¨™æº–ãƒ©ã‚¤ãƒ–ãƒ©ãƒªã§åˆ©ç”¨å¯èƒ½ãªãƒ©ã‚¤ãƒ†ã‚£ãƒ³ã‚°é–¢æ•°ã‚’ç”¨ã„ã¦ï¼Œãƒ©ã‚¤ãƒ†ã‚£ãƒ³ã‚°å®Ÿè£…ã®æ‰‹é–“ã‚’å‰Šæ¸›ã™ã‚‹æ–¹æ³•ã«ã¤ã„ã¦è¿°ã¹ã‚‹ï¼

ã“ã“ã§ç´¹ä»‹ã™ã‚‹ã®ã¯Unityã®æ¨™æº–ãƒ©ã‚¤ãƒ–ãƒ©ãƒªã§æä¾›ã•ã‚Œã¦ã„ã‚‹4ã¤ã®ãƒ©ã‚¤ãƒ†ã‚£ãƒ³ã‚°é–¢æ•°

- Lambert
- Blinn-Phong
- Standard
- Standard Specular

ã‚’ç”¨ã„ãŸä¾‹ã§ã‚ã‚‹ï¼
ä¸‹è¨˜ã®ç”»åƒã¯å·¦ã‹ã‚‰é †ã«Lambert, Blinn-Phong, Standard, Standard Specularã®ä¾‹ã¨ãªã£ã¦ã„ã‚‹ï¼

[![UnityLighting.png](/images/koturn-unity-sphere-tracing-tips/UnityLighting.png)](/images/koturn-unity-sphere-tracing-tips/UnityLighting.png "UnityLighting.png")

### Lambert

ã‚µãƒ³ãƒ—ãƒ«ã‚³ãƒ¼ãƒ‰: [Assets/koturn/SphereTracingTips/03_Lighting/Shaders/Lambert.shader](https://github.com/koturn/SphereTracingTips/blob/main/Assets/koturn/SphereTracingTips/03_Lighting/Shaders/Lambert.shader "Assets/koturn/SphereTracingTips/03_Lighting/Shaders/Lambert.shader")

Lambertç¨‹åº¦ã§ã‚ã‚Œã°è‡ªå‰å®Ÿè£…ã—ã¦ã‚‚ã‚ˆã„ã®ã ãŒï¼Œãƒ©ã‚¤ãƒ–ãƒ©ãƒªã¨ã—ã¦æä¾›ã•ã‚Œã¦ã„ã‚‹ãŸã‚ï¼Œä½¿ç”¨ã™ã‚‹ä¾‹ã‚’ç¤ºã™ï¼

```hlsl
/*!
 * Calculate lighting.
 * @param [in] color  Base color.
 * @param [in] worldPos  World coordinate.
 * @param [in] worldNormal  Normal in world space.
 * @param [in] atten  Light attenuation.
 * @param [in] ambient  Ambient light.
 * @return Lighting applied color.
 */
half4 calcLighting(half4 color, float3 worldPos, float3 worldNormal, half atten, half3 ambient)
{
    SurfaceOutput so;
    UNITY_INITIALIZE_OUTPUT(SurfaceOutput, so);
    so.Albedo = color.rgb;
    so.Normal = worldNormal;
    so.Emission = fixed3(0.0, 0.0, 0.0);
    // so.Specular = 0.0;
    // so.Gloss = 0.0;
    so.Alpha = color.a;

    UnityGI gi = getGI(worldPos, atten);
    const float3 worldViewDir = normalize(UnityWorldSpaceViewDir(worldPos));
#if defined(UNITY_PASS_FORWARDBASE)
    const float4 lmap = float4(0.0, 0.0, 0.0, 0.0);
    UnityGIInput giInput = getGIInput(gi.light, worldPos, worldNormal, worldViewDir, atten, lmap, ambient);
    LightingLambert_GI(so, giInput, /* inout */ gi);
#endif  // defined(UNITY_PASS_FORWARDBASE)

    half4 col = LightingLambert(so, gi);
#if defined(UNITY_PASS_FORWARDBASE)
    col.rgb += so.Emission;
#endif  // defined(UNITY_PASS_FORWARDBASE)

    return col;
}
```

ã¾ãŸï¼Œ `getGI()` ã¨ `getGIInput()` ã®å®Ÿè£…ã¯ä¸‹è¨˜ã®ã¨ãŠã‚Šï¼
ã“ã‚Œã¯ä»–ã®ä¾‹ã§ã‚‚ç”¨ã„ã‚‹ï¼

```hlsl
/*!
 * @brief Get initial instance of UnityGI.
 * @param [in] worldPos  World coordinate.
 * @param [in] atten  Light attenuation.
 * @return Initial instance of UnityGI.
 */
UnityGI getGI(float3 worldPos, half atten)
{
    UnityGI gi;
    UNITY_INITIALIZE_OUTPUT(UnityGI, gi);

#if defined(UNITY_PASS_FORWARDBASE)
    gi.light.color = _LightColor0.rgb;
#elif defined(UNITY_PASS_DEFERRED)
    gi.light.color = half3(0.0, 0.0, 0.0);
#else
    gi.light.color = _LightColor0.rgb * atten;
#endif  // defined(UNITY_PASS_FORWARDBASE)
#if defined(UNITY_PASS_DEFERRED)
    gi.light.dir = half3(0.0, 1.0, 0.0);
#elif defined(USING_LIGHT_MULTI_COMPILE) && defined(USING_DIRECTIONAL_LIGHT)
    gi.light.dir = UnityWorldSpaceLightDir(worldPos);
#else
    gi.light.dir = normalize(UnityWorldSpaceLightDir(worldPos));
#endif  // defined(UNITY_PASS_DEFERRED)
    // gi.indirect.diffuse = half3(0.0, 0.0, 0.0);
    // gi.indirect.specular = half3(0.0, 0.0, 0.0);

    return gi;
}


/*!
 * @brief Get initial instance of UnityGIInput.
 * @param [in] light  The lighting parameter which contains color and direction of the light.
 * @param [in] worldPos  World coordinate.
 * @param [in] worldNormal  Normal in world space.
 * @param [in] worldViewDir  View direction in world space.
 * @param [in] atten  Light attenuation.
 * @param [in] lmap  Light map parameters.
 * @param [in] ambient  Ambient light.
 * @return Initial instance of UnityGIInput.
 */
UnityGIInput getGIInput(UnityLight light, float3 worldPos, float3 worldNormal, float3 worldViewDir, half atten, float4 lmap, half3 ambient)
{
    UnityGIInput giInput;
    UNITY_INITIALIZE_OUTPUT(UnityGIInput, giInput);
    giInput.light = light;
    giInput.worldPos = worldPos;
    giInput.worldViewDir = worldViewDir;
    giInput.atten = atten;

#if defined(LIGHTMAP_ON) || defined(DYNAMICLIGHTMAP_ON)
    giInput.lightmapUV = lmap;
#else
    giInput.lightmapUV = float4(0.0, 0.0, 0.0, 0.0);
#endif  // defined(LIGHTMAP_ON) || defined(DYNAMICLIGHTMAP_ON)

#if UNITY_SHOULD_SAMPLE_SH
    giInput.ambient = ambient;
#else
    giInput.ambient = half3(0.0, 0.0, 0.0);
#endif  // UNITY_SHOULD_SAMPLE_SH

    giInput.probeHDR[0] = unity_SpecCube0_HDR;
    giInput.probeHDR[1] = unity_SpecCube1_HDR;
#if defined(UNITY_SPECCUBE_BLENDING) || defined(UNITY_SPECCUBE_BOX_PROJECTION)
    giInput.boxMin[0] = unity_SpecCube0_BoxMin;
#endif  // defined(UNITY_SPECCUBE_BLENDING) || defined(UNITY_SPECCUBE_BOX_PROJECTION)
#if defined(UNITY_SPECCUBE_BOX_PROJECTION)
    giInput.boxMax[0] = unity_SpecCube0_BoxMax;
    giInput.probePosition[0] = unity_SpecCube0_ProbePosition;
    giInput.boxMax[1] = unity_SpecCube1_BoxMax;
    giInput.boxMin[1] = unity_SpecCube1_BoxMin;
    giInput.probePosition[1] = unity_SpecCube1_ProbePosition;
#endif  // defined(UNITY_SPECCUBE_BOX_PROJECTION)

    return giInput;
}
```

### Blinn Phong

ã‚µãƒ³ãƒ—ãƒ«ã‚³ãƒ¼ãƒ‰: [Assets/koturn/SphereTracingTips/03_Lighting/Shaders/BlinnPhong.shader](https://github.com/koturn/SphereTracingTips/blob/main/Assets/koturn/SphereTracingTips/03_Lighting/Shaders/BlinnPhong.shader "Assets/koturn/SphereTracingTips/03_Lighting/Shaders/BlinnPhong.shader")

Lambertã¨åŒæ§˜ã« `SurfaceOutput` æ§‹é€ ä½“ã‚’ç”¨ã„ã‚‹ãŒï¼Œä½¿ç”¨ã™ã‚‹é–¢æ•°ãŒ `LightingBlinnPhong_GI()` ã¨ `LightingBlinnPhong()` ã§ã‚ã‚‹ç‚¹ãŒç•°ãªã£ã¦ã„ã‚‹ï¼

```hlsl
/*!
 * Calculate lighting.
 * @param [in] color  Base color.
 * @param [in] worldPos  World coordinate.
 * @param [in] worldNormal  Normal in world space.
 * @param [in] atten  Light attenuation.
 * @param [in] ambient  Ambient light.
 * @return Lighting applied color.
 */
half4 calcLighting(half4 color, float3 worldPos, float3 worldNormal, half atten, half3 ambient)
{
    SurfaceOutput so;
    UNITY_INITIALIZE_OUTPUT(SurfaceOutput, so);
    so.Albedo = color.rgb;
    so.Normal = worldNormal;
    so.Emission = fixed3(0.0, 0.0, 0.0);
    so.Specular = _SpecPower / 128.0;
    so.Gloss = _Glossiness;
    so.Alpha = color.a;

    UnityGI gi = getGI(worldPos, atten);
    const float3 worldViewDir = normalize(UnityWorldSpaceViewDir(worldPos));
#if defined(UNITY_PASS_FORWARDBASE)
    const float4 lmap = float4(0.0, 0.0, 0.0, 0.0);
    UnityGIInput giInput = getGIInput(gi.light, worldPos, worldNormal, worldViewDir, atten, lmap, ambient);
    LightingBlinnPhong_GI(so, giInput, /* inout */ gi);
#endif  // defined(UNITY_PASS_FORWARDBASE)

    half4 col = LightingBlinnPhong(so, worldViewDir, gi);
#if defined(UNITY_PASS_FORWARDBASE)
    col.rgb += so.Emission;
#endif  // defined(UNITY_PASS_FORWARDBASE)

    return col;
}
```

### Standard

ã‚µãƒ³ãƒ—ãƒ«ã‚³ãƒ¼ãƒ‰: [Assets/koturn/SphereTracingTips/03_Lighting/Shaders/Standard.shader](https://github.com/koturn/SphereTracingTips/blob/main/Assets/koturn/SphereTracingTips/03_Lighting/Shaders/Standard.shader "Assets/koturn/SphereTracingTips/03_Lighting/Shaders/Standard.shader")

Standardã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ç›¸å½“ã®ãƒ©ã‚¤ãƒ†ã‚£ãƒ³ã‚°ã‚’è¡Œã†ã«ã¯ï¼Œæ¨™æº–ãƒ©ã‚¤ãƒ–ãƒ©ãƒªãŒæä¾›ã—ã¦ã„ã‚‹ `LightingStandard_GI()` ã¨ `LightingStandard()` ã‚’ç”¨ã„ã‚‹ã¨ã‚ˆã„ï¼
ãã®ãŸã‚ `calcLighting()` ã‚’ä¸‹è¨˜ã®ã‚ˆã†ã«å¤‰æ›´ã™ã‚‹ï¼

```hlsl
/*!
 * Calculate lighting.
 * @param [in] color  Base color.
 * @param [in] worldPos  World coordinate.
 * @param [in] worldNormal  Normal in world space.
 * @param [in] atten  Light attenuation.
 * @param [in] ambient  Ambient light.
 * @return Lighting applied color.
 */
half4 calcLighting(half4 color, float3 worldPos, float3 worldNormal, half atten, half3 ambient)
{
    SurfaceOutputStandard so;
    UNITY_INITIALIZE_OUTPUT(SurfaceOutputStandard, so);
    so.Albedo = color.rgb;
    so.Normal = worldNormal;
    so.Emission = half3(0.0, 0.0, 0.0);
    so.Metallic = _Metallic;
    so.Smoothness = _Glossiness;
    so.Occlusion = 1.0;
    so.Alpha = color.a;

    UnityGI gi = getGI(worldPos, atten);
    const float3 worldViewDir = normalize(UnityWorldSpaceViewDir(worldPos));
#if defined(UNITY_PASS_FORWARDBASE)
    const float4 lmap = float4(0.0, 0.0, 0.0, 0.0);
    UnityGIInput giInput = getGIInput(gi.light, worldPos, worldNormal, worldViewDir, atten, lmap, ambient);
    LightingStandard_GI(so, giInput, /* inout */ gi);
#endif  // defined(UNITY_PASS_FORWARDBASE)

    half4 col = LightingStandard(so, worldViewDir, gi);
#if defined(UNITY_PASS_FORWARDBASE)
    col.rgb += so.Emission;
#endif  // defined(UNITY_PASS_FORWARDBASE)

    return col;
}
```

å¿…è¦ã¨ãªã‚‹ãƒ—ãƒ­ãƒ‘ãƒ†ã‚£ã¯ `_Glossiness`, `_Metallic` ã®2ã¤ã§ã‚ã‚‹ï¼

`Properties` ã«ä¸‹è¨˜ã‚’ï¼Œ

```shaderlab
_Glossiness ("Smoothness", Range(0.0, 1.0)) = 0.5

[Gamma]
_Metallic ("Metallic", Range(0.0, 1.0)) = 0.0
```

ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã‚³ãƒ¼ãƒ‰éƒ¨åˆ†ã«ä¸‹è¨˜ã‚’è¨˜è¿°ã—ã¦ãŠãï¼

```hlsl
//! Value of smoothness.
uniform half _Glossiness;
//! Value of Metallic.
uniform half _Metallic;
```

ãªãŠï¼Œ `_GLOSSYREFLECTIONS_OFF` ãƒã‚¯ãƒ­ãŒå®šç¾©ã•ã‚Œã¦ã„ã‚‹å ´åˆï¼Œãƒªãƒ•ãƒ¬ã‚¯ã‚·ãƒ§ãƒ³ãƒ—ãƒ­ãƒ¼ãƒ–ã®åæ˜ ã‚’è¡Œã‚ãªã„ï¼Œ
`_SPECULARHIGHLIGHTS_OFF` ãƒã‚¯ãƒ­ãŒå®šç¾©ã•ã‚Œã¦ã„ã‚‹å ´åˆï¼Œé¡é¢åå°„ã‚’è¡Œã‚ãªã„ã‚ˆã†ã«ãªã£ã¦ã„ã‚‹ã®ã§ï¼Œä¸‹è¨˜ã®å®£è¨€ãŒã‚ã‚‹ã¨ã‚ˆã‚ŠStandardã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã®æ©Ÿèƒ½ã‚’å…·å‚™ã—ãŸå½¢ã¨ãªã‚‹ï¼

```shaderlab
[ToggleOff(_SPECULARHIGHLIGHTS_OFF)]
_SpecularHighlights ("Specular Highlights", Int) = 1

[ToggleOff(_GLOSSYREFLECTIONS_OFF)]
_GlossyReflections ("Glossy Reflections", Int) = 1
```

```hlsl
#pragma shader_feature_local_fragment _ _SPECULARHIGHLIGHTS_OFF
#pragma shader_feature_local_fragment _ _GLOSSYREFLECTIONS_OFF
```

### Standard Specular

ã‚µãƒ³ãƒ—ãƒ«ã‚³ãƒ¼ãƒ‰: [Assets/koturn/SphereTracingTips/03_Lighting/Shaders/StandardSpecular.shader](https://github.com/koturn/SphereTracingTips/blob/main/Assets/koturn/SphereTracingTips/03_Lighting/Shaders/StandardSpecular.shader "Assets/koturn/SphereTracingTips/03_Lighting/Shaders/StandardSpecular.shader")

Standardã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ç›¸å½“ã®ãƒ©ã‚¤ãƒ†ã‚£ãƒ³ã‚°ã‚’è¡Œã†ã«ã¯ï¼Œæ¨™æº–ãƒ©ã‚¤ãƒ–ãƒ©ãƒªãŒæä¾›ã—ã¦ã„ã‚‹ `LightingStandardSpecular_GI()` ã¨ `LightingStandardSpecular()` ã‚’ç”¨ã„ã‚‹ã¨ã‚ˆã„ï¼
ãã®ãŸã‚ `calcLighting()` ã‚’ä¸‹è¨˜ã®ã‚ˆã†ã«å¤‰æ›´ã™ã‚‹ï¼

```hlsl
/*!
 * Calculate lighting.
 * @param [in] color  Base color.
 * @param [in] worldPos  World coordinate.
 * @param [in] worldNormal  Normal in world space.
 * @param [in] atten  Light attenuation.
 * @param [in] ambient  Ambient light.
 * @return Lighting applied color.
 */
half4 calcLighting(half4 color, float3 worldPos, float3 worldNormal, half atten, half3 ambient)
{
    SurfaceOutputStandardSpecular so;
    UNITY_INITIALIZE_OUTPUT(SurfaceOutputStandardSpecular, so);
    so.Albedo = color.rgb;
    so.Specular = _SpecColor.rgb;
    so.Normal = worldNormal;
    so.Emission = half3(0.0, 0.0, 0.0);
    so.Smoothness = _Glossiness;
    so.Occlusion = 1.0;
    so.Alpha = color.a;

    UnityGI gi = getGI(worldPos, atten);
    const float3 worldViewDir = normalize(UnityWorldSpaceViewDir(worldPos));
#if defined(UNITY_PASS_FORWARDBASE)
    const float4 lmap = float4(0.0, 0.0, 0.0, 0.0);
    UnityGIInput giInput = getGIInput(gi.light, worldPos, worldNormal, worldViewDir, atten, lmap, ambient);
    LightingStandardSpecular_GI(so, giInput, /* inout */ gi);
#endif  // defined(UNITY_PASS_FORWARDBASE)

    half4 col = LightingStandardSpecular(so, worldViewDir, gi);
#if defined(UNITY_PASS_FORWARDBASE)
    col.rgb += so.Emission;
#endif  // defined(UNITY_PASS_FORWARDBASE)

    return col;
}
```

å¿…è¦ã¨ãªã‚‹ãƒ—ãƒ­ãƒ‘ãƒ†ã‚£ã¯ `_SpecColor` ã¨ `_Glossiness` ã®2ã¤ã§ã‚ã‚‹ï¼
Standardã¨ç•°ãªã‚Šï¼Œ `_Metallic` ã¯ä¸è¦ã§ã‚ã‚‹ï¼

`Properties` ã«ä¸‹è¨˜ã‚’ï¼Œ

```shaderlab
_SpecColor ("Specular Color", Color) = (0.5, 0.5, 0.5, 1.0)

_Glossiness ("Smoothness", Range(0.0, 1.0)) = 0.5
```

ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã‚³ãƒ¼ãƒ‰éƒ¨åˆ†ã«ä¸‹è¨˜ã‚’è¨˜è¿°ã—ã¦ãŠãï¼
`_SpecColor` ã¯ UnityLightingCommon.cginc ã§å®£è¨€ã•ã‚Œã¦ã„ã‚‹ãŸã‚ä¸è¦ã§ã‚ã‚‹ï¼

```hlsl
//! Value of smoothness.
uniform half _Glossiness;
```

### å…¨éƒ¨å…¥ã‚Š

ã‚µãƒ³ãƒ—ãƒ«ã‚³ãƒ¼ãƒ‰: [Assets/koturn/SphereTracingTips/03_Lighting/Shaders/AllInOne.shader](https://github.com/koturn/SphereTracingTips/blob/main/Assets/koturn/SphereTracingTips/03_Lighting/Shaders/AllInOne.shader "Assets/koturn/SphereTracingTips/03_Lighting/Shaders/AllInOne.shader")

æœ¬ç« ã§ç´¹ä»‹ã—ãŸ4ã¤

- Lambert
- Blinn-Phong
- Standard
- Standard Specular

ã®ã„ãšã‚Œã‚‚Unityã®BRPã®ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼æ¨™æº–ãƒ©ã‚¤ãƒ–ãƒ©ãƒªãŒæä¾›ã™ã‚‹ãƒ©ã‚¤ãƒ†ã‚£ãƒ³ã‚°é–¢æ•°ã®ã‚¤ãƒ³ã‚¿ãƒ¼ãƒ•ã‚§ãƒ¼ã‚¹ã¯ã»ã¼åŒã˜ã§ã‚ã‚‹ãŸã‚ï¼Œãƒã‚¯ãƒ­å®šç¾©ã§å·®ã‚’å¸åã—ã‚„ã™ã„ï¼
ã“ã‚Œã‚‰4ã¤ã®ãƒ©ã‚¤ãƒ†ã‚£ãƒ³ã‚°ã¨ï¼Œè¨˜äº‹å†’é ­ã®ãƒ™ãƒ¼ã‚¹ã«ã—ã¦ã„ã‚‹ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã®Half-Lambert + Blinn-Phongã®ãƒ©ã‚¤ãƒ†ã‚£ãƒ³ã‚°ï¼Œãã‚Œã«åŠ ãˆã¦Unlitã‚’ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã‚­ãƒ¼ãƒ¯ãƒ¼ãƒ‰ã§åˆ‡ã‚Šæ›¿ãˆå¯èƒ½ã«ã—ãŸä¾‹ã‚’æŠœç²‹ã—ã¦ç¤ºã™ï¼

ã‚­ãƒ¼ãƒ¯ãƒ¼ãƒ‰ã«ã‚ˆã£ã¦ãƒ©ã‚¤ãƒ†ã‚£ãƒ³ã‚°ãƒ¢ãƒ‡ãƒ«ã‚’åˆ‡ã‚Šæ›¿ãˆãŸã„å ´åˆã¯ä¸‹è¨˜ã®ã‚ˆã†ã«ãªã‚‹ï¼
`calcLighting()` ã‚’ã•ã‚‰ã« `calcLightingUnity()` ã¨ `calcLightingCustom()` ã«ç´°åˆ†åŒ–ã—ãŸï¼

```hlsl
/*!
 * Calculate lighting.
 * @param [in] color  Base color.
 * @param [in] worldPos  World coordinate.
 * @param [in] worldNormal  Normal in world space.
 * @param [in] atten  Light attenuation.
 * @param [in] ambient  Ambient light.
 * @return Lighting applied color.
 */
half4 calcLighting(half4 color, float3 worldPos, float3 worldNormal, half atten, half3 ambient)
{
#if defined(_LIGHTING_CUSTOM)
    return calcLightingCustom(color, worldPos, worldNormal, atten, ambient);
#elif defined(_LIGHTING_UNITY_LAMBERT) \
    || defined(_LIGHTING_UNITY_BLINN_PHONG) \
    || defined(_LIGHTING_UNITY_STANDARD) \
    || defined(_LIGHTING_UNITY_STANDARD_SPECULAR)
    return calcLightingUnity(color, worldPos, worldNormal, atten, ambient);
#else
    // assume _LIGHTING_UNLIT
    return color;
#endif  // defined(_LIGHTING_CUSTOM)
}

/*!
 * Calculate lighting.
 * @param [in] color  Base color.
 * @param [in] worldPos  World coordinate.
 * @param [in] worldNormal  Normal in world space.
 * @param [in] atten  Light attenuation.
 * @param [in] ambient  Ambient light.
 * @return Lighting applied color.
 */
half4 calcLightingUnity(half4 color, float3 worldPos, float3 worldNormal, half atten, half3 ambient)
{
    // Uniform variable requirements:
    //
    // Variant                             | `_Glossiness` | `_Metallic` | `_SpecColor` | `_SpecPower`
    // ------------------------------------|---------------|-------------|--------------|-------------
    // `_LIGHTING_UNITY_LAMBERT`           |               |             |              |
    // `_LIGHTING_UNITY_BLINN_PHONG`       | o             |             | o            | o
    // `_LIGHTING_UNITY_STANDARD`          | o             | o           |              |
    // `_LIGHTING_UNITY_STANDARD_SPECULAR` | o             |             | o            |

#if defined(_LIGHTING_UNITY_STANDARD)
#    define LightingUnity_GI(so, giInput, gi) LightingStandard_GI(so, giInput, gi)
#    define LightingUnity(so, worldViewDir, gi) LightingStandard(so, worldViewDir, gi)
    SurfaceOutputStandard so;
    UNITY_INITIALIZE_OUTPUT(SurfaceOutputStandard, so);
    so.Albedo = color.rgb;
    so.Normal = worldNormal;
    so.Emission = half3(0.0, 0.0, 0.0);
    so.Metallic = _Metallic;
    so.Smoothness = _Glossiness;
    so.Occlusion = 1.0;
    so.Alpha = color.a;
#elif defined(_LIGHTING_UNITY_STANDARD_SPECULAR)
#    define LightingUnity_GI(so, giInput, gi) LightingStandardSpecular_GI(so, giInput, gi)
#    define LightingUnity(so, worldViewDir, gi) LightingStandardSpecular(so, worldViewDir, gi)
    SurfaceOutputStandardSpecular so;
    UNITY_INITIALIZE_OUTPUT(SurfaceOutputStandardSpecular, so);
    so.Albedo = color.rgb;
    so.Specular = _SpecColor.rgb;
    so.Normal = worldNormal;
    so.Emission = half3(0.0, 0.0, 0.0);
    so.Smoothness = _Glossiness;
    so.Occlusion = 1.0;
    so.Alpha = color.a;
#else
    SurfaceOutput so;
    UNITY_INITIALIZE_OUTPUT(SurfaceOutput, so);
    so.Albedo = color.rgb;
    so.Normal = worldNormal;
    so.Emission = fixed3(0.0, 0.0, 0.0);
#    if defined(_LIGHTING_UNITY_BLINN_PHONG)
#        define LightingUnity_GI(so, giInput, gi) LightingBlinnPhong_GI(so, giInput, gi)
#        define LightingUnity(so, worldViewDir, gi) LightingBlinnPhong(so, worldViewDir, gi)
    so.Specular = _SpecPower / 128.0;
    so.Gloss = _Glossiness;
    // NOTE: _SpecColor is used in UnityBlinnPhongLight() used in LightingBlinnPhong().
#    else
#        define LightingUnity_GI(so, giInput, gi) LightingLambert_GI(so, giInput, gi)
#        define LightingUnity(so, worldViewDir, gi) LightingLambert(so, gi)
#    endif  // defined(_LIGHTING_UNITY_BLINN_PHONG)
    so.Alpha = color.a;
#endif  // defined(_LIGHTING_UNITY_STANDARD)

    UnityGI gi = getGI(worldPos, atten);
    const float3 worldViewDir = normalize(UnityWorldSpaceViewDir(worldPos));
#if defined(UNITY_PASS_FORWARDBASE)
    const float4 lmap = float4(0.0, 0.0, 0.0, 0.0);
    UnityGIInput giInput = getGIInput(gi.light, worldPos, worldNormal, worldViewDir, atten, lmap, ambient);
    LightingUnity_GI(so, giInput, /* inout */ gi);
#endif  // defined(UNITY_PASS_FORWARDBASE)

    half4 col = LightingUnity(so, worldViewDir, gi);
#if defined(UNITY_PASS_FORWARDBASE)
    col.rgb += so.Emission;
#endif  // defined(UNITY_PASS_FORWARDBASE)

    return col;

#undef LightingUnity_GI
#undef LightingUnity
}

/*!
 * Calculate lighting with custom method.
 * @param [in] color  Base color.
 * @param [in] worldPos  World coordinate.
 * @param [in] worldNormal  Normal in world space.
 * @param [in] atten  Light attenuation.
 * @param [in] ambient  Ambient light.
 * @return Lighting applied color.
 */
half4 calcLightingCustom(half4 color, float3 worldPos, float3 worldNormal, half atten, half3 ambient)
{
    const float3 worldViewDir = normalize(_WorldSpaceCameraPos - worldPos);
#if defined(USING_LIGHT_MULTI_COMPILE) && defined(USING_DIRECTIONAL_LIGHT)
    const float3 worldLightDir = UnityWorldSpaceLightDir(worldPos);
#else
    const float3 worldLightDir = normalize(UnityWorldSpaceLightDir(worldPos));
#endif  // defined(USING_LIGHT_MULTI_COMPILE) && defined(USING_DIRECTIONAL_LIGHT)
    const fixed3 lightCol = _LightColor0.rgb * atten;

    // Lambertian reflectance.
    const float nDotL = dot(worldNormal, worldLightDir);
    const half3 diffuse = lightCol * pow(nDotL * 0.5 + 0.5, 2.0);  // will be mul instruction.

    // Specular reflection.
    const float nDotH = dot(worldNormal, normalize(worldLightDir + worldViewDir));
    const half3 specular = pow(max(0.0, nDotH), _SpecPower) * _SpecColor.rgb * lightCol;

    // Ambient color.
#if UNITY_SHOULD_SAMPLE_SH
    ambient = ShadeSHPerPixel(worldNormal, ambient, worldPos);
#endif  // UNITY_SHOULD_SAMPLE_SH

    const half4 outColor = half4((diffuse + ambient) * color.rgb + specular, color.a);

    return outColor;
}
```

Propertiesã®å®£è¨€ã«ã¯ä¸‹è¨˜ãŒå¿…è¦ï¼

```shaderlab
[KeywordEnum(Unity Lambert, Unity Blinn Phong, Unity Standard, Unity Standard Specular, Unlit, Custom)]
_Lighting ("Lighting method", Int) = 2
```

ã¾ãŸï¼Œã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã‚­ãƒ¼ãƒ¯ãƒ¼ãƒ‰ã®ãƒ—ãƒ©ã‚°ãƒã¯ä¸‹è¨˜ã®ã¨ãŠã‚Šï¼

```hlsl
#pragma shader_feature_local_fragment _LIGHTING_UNITY_LAMBERT _LIGHTING_UNITY_BLINN_PHONG _LIGHTING_UNITY_STANDARD _LIGHTING_UNITY_STANDARD_SPECULAR _LIGHTING_UNLIT _LIGHTING_CUSTOM
```

ãƒ©ã‚¤ãƒ†ã‚£ãƒ³ã‚°ãƒ¢ãƒ‡ãƒ«ã«ã‚ˆã‚Šå¿…è¦ã¨ã™ã‚‹ãƒ—ãƒ­ãƒ‘ãƒ†ã‚£ãŒç•°ãªã‚‹ãŒï¼Œè¡¨ã«ã¾ã¨ã‚ã‚‹ã¨ä¸‹è¨˜ã®ã¨ãŠã‚Šã§ã‚ã‚‹ï¼

| ãƒ©ã‚¤ãƒ†ã‚£ãƒ³ã‚°åç§° | ã‚­ãƒ¼ãƒ¯ãƒ¼ãƒ‰ | `_Glossiness` | `_Metallic` | `_SpecColor` | `_SpecPower` |
|-|-|:-:|:-:|:-:|:-:|
| Lambert | `_LIGHTING_UNITY_LAMBERT` | | | | |
| Blinn-Phong | `_LIGHTING_UNITY_BLINN_PHONG` | ã€‡ | | ã€‡ | ã€‡ |
| Standard | `_LIGHTING_UNITY_STANDARD` | ã€‡ | ã€‡ | | |
| Standard Specular | `_LIGHTING_UNITY_STANDARD_SPECULAR` | ã€‡ | | ã€‡ | |
| Unlit | `_LIGHTING_UNLIT` | | | | |
| Custom | `_LIGHTING_CUSTOM` | | | ã€‡ | ã€‡ |

---

## VRChatç‰¹æœ‰ã®ãƒ©ã‚¤ãƒ†ã‚£ãƒ³ã‚°

ãƒ©ã‚¤ãƒ†ã‚£ãƒ³ã‚°ã®è©±é¡Œã®ç¶šãã§ã‚ã‚‹ãŸã‚ï¼Œæœ¬ç« ã¯å‰ç« ã®[å…¨éƒ¨å…¥ã‚Šã®ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼](https://github.com/koturn/SphereTracingTips/blob/main/Assets/koturn/SphereTracingTips/03_Lighting/Shaders/AllInOne.shader "Assets/koturn/SphereTracingTips/03_Lighting/Shaders/AllInOne.shader")ã‚’ãƒ™ãƒ¼ã‚¹ã«æ”¹é€ ã™ã‚‹ã“ã¨ã¨ã™ã‚‹ï¼

### VRC Light Volumes

ã‚µãƒ³ãƒ—ãƒ«ã‚³ãƒ¼ãƒ‰: [Assets/koturn/SphereTracingTips/04_LightingSpecial/Shaders/VRCLV.shader](https://github.com/koturn/SphereTracingTips/blob/main/Assets/koturn/SphereTracingTips/04_LightingSpecial/Shaders/VRCLV.shader "Assets/koturn/SphereTracingTips/04_LightingSpecial/Shaders/VRCLV.shader")

[VRC Light Volumes](https://github.com/REDSIM/VRCLightVolumes "REDSIM/VRCLightVolumes")ã«å¯¾å¿œã™ã‚‹ã«ã¯[å…¬å¼ã®ãƒ‰ã‚­ãƒ¥ãƒ¡ãƒ³ãƒˆã«è¨˜è¼‰ãŒã‚ã‚‹ã‚ˆã†ã«](https://github.com/REDSIM/VRCLightVolumes/blob/v.2.0.1/Documentation/ForShaderDevelopers.md "REDSIM/VRCLightVolumes Documentation/ForShaderDevelopers.md")ï¼Œä¸‹è¨˜ã®å¯¾å¿œã‚’è¡Œã†ã¨ã‚ˆã„ï¼

1. `LightVolumeSH()` ã§çƒé¢èª¿å’Œé–¢æ•°ã®ä¿‚æ•°ã‚’è¨ˆç®—
2. `ShaderSH9()` ã‚ã‚‹ã„ã¯ `ShadeSHPerPixel()` ã®ä»£ã‚ã‚Šã« `LightVolumeEvaluate()` ã‚’ç”¨ã„ã¦diffuseã‚’è¨ˆç®—
3. `LightVolumeSpecular()` ã¾ãŸã¯ `LightVolumeSpecularDominant()` ã‚’ç”¨ã„ã¦specularã‚’è¨ˆç®—ï¼ˆå¿…é ˆã§ã¯ãªã„ï¼‰
4. å‡ºåŠ›ã‚«ãƒ©ãƒ¼ã«diffuseã¨specularã‚’åæ˜ 

å…¬å¼ã«[Surfaceã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã®ã‚µãƒ³ãƒ—ãƒ«ã‚³ãƒ¼ãƒ‰](https://github.com/REDSIM/VRCLightVolumes/blob/v.2.0.1/Packages/red.sim.lightvolumes/Shaders/ASE%20Shaders/Light%20Volume%20PBR.shader "REDSIM/VRCLightVolumes Packages/red.sim.lightvolumes/Shaders/ASE Shaders/Light Volume PBR.shader")ãŒã‚ã‚‹ãŒï¼Œ[Amplified Shader Editor](https://assetstore.unity.com/packages/tools/visual-scripting/amplify-shader-editor-68570 "Amplify Shader Editor | Visual Scripting | Unity Asset Store")è£½ã§ã‚ã‚‹ãŸã‚ï¼Œäººé–“ã«å„ªã—ããªã„ã‚³ãƒ¼ãƒ‰ã¨ãªã£ã¦ã„ã‚‹ï¼
[äººé–“å‘ã‘ã«å„ªã—ãã—ãŸã‚³ãƒ¼ãƒ‰ã‚’gistã«ç½®ã„ãŸ](https://gist.github.com/koturn/2626d9e33d3fc2b0ea2237c5afe6d8a3 "https://gist.github.com/koturn/2626d9e33d3fc2b0ea2237c5afe6d8a3")ã®ã§ï¼Œã‚³ãƒ¼ãƒ‰ã¨ã—ã¦å‚ç…§ã—ãŸã„å ´åˆã¯ã“ã¡ã‚‰ã‚’è¦‹ã‚‹ã“ã¨ã‚’æ¨å¥¨ã™ã‚‹ï¼

ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã‚³ãƒ¼ãƒ‰ã¨ã—ã¦ã¯ï¼Œã¾ãšæœ‰åŠ¹ãƒ»ç„¡åŠ¹ã‚’åˆ‡ã‚Šæ›¿ãˆã‚‹ãŸã‚ã®ãƒ—ãƒ­ãƒ‘ãƒ†ã‚£ã¨ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã‚­ãƒ¼ãƒ¯ãƒ¼ãƒ‰ã‚’è¨­ã‘ãŸï¼
Additiveã«é–¢ã—ã¦ã¯ãƒ©ã‚¤ãƒˆãƒãƒƒãƒ—ã‚’ä½¿ç”¨ã™ã‚‹é™çš„ãªã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã«å¯¾ã—ã¦ä½¿ç”¨ã™ã‚‹ã‚‚ã®ãªã®ã§ï¼Œãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã§ã®ç”¨é€”ã¯ãªã„ãŒï¼ŒVRC Light Volumesã®é–¢æ•°éƒ¨åˆ†ã ã‘ä»–ã®ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã«ãã®ã¾ã¾è»¢ç”¨ã§ãã‚‹ã‚ˆã†ã«ã—ãŸã‹ã£ãŸã®ã§ä¸€å¿œæ®‹ã—ã¦ã„ã‚‹ï¼

```shaderlab
[KeywordEnum(Off, On, Additive Only)]
_VRCLightVolumes ("VRC Light Volumes", Int) = 0

[KeywordEnum(Off, On, Dominant Dir)]
_VRCLightVolumesSpecular ("VRC Light Volumes Specular", Int) = 0
```

```hlsl
#pragma shader_feature_local_fragment _VRCLIGHTVOLUMES_OFF _VRCLIGHTVOLUMES_ON _VRCLIGHTVOLUMES_ADDITIVE_ONLY
#pragma shader_feature_local_fragment _VRCLIGHTVOLUMESSPECULAR_OFF _VRCLIGHTVOLUMESSPECULAR_ON _VRCLIGHTVOLUMESSPECULAR_DOMINANT_DIR
```

`LightVolumes.cginc` ã‚’ã‚¤ãƒ³ã‚¯ãƒ«ãƒ¼ãƒ‰ã™ã¹ãã‹ï¼ŒVRC Light Volumesã®å‡¦ç†ã‚’é€šã™ã¹ãã‹ã‚’åˆ¤æ–­ã™ã‚‹ã®ã«4ã¤ã®ãƒã‚¯ãƒ­ã®ã„ãšã‚Œã‹ãŒæœ‰åŠ¹ã§ã‚ã‚‹ã“ã¨ã‚’éšæ‰€ã§åˆ¤å®šã™ã‚‹ã®ã¯ç…©é›‘ãªã®ã§ï¼Œä¸‹è¨˜ã®ãƒã‚¯ãƒ­ `USE_VRCLIGHTVOLUMES` ã‚’è¨­ã‘ãŸï¼

```hlsl
#if defined(_VRCLIGHTVOLUMES_ON) || defined(_VRCLIGHTVOLUMES_ADDITIVE_ONLY) || defined(_VRCLIGHTVOLUMESSPECULAR_ON) || defined(_VRCLIGHTVOLUMESSPECULAR_DOMINANT_DIR)
#    define USE_VRCLIGHTVOLUMES
#endif  // defined(_VRCLIGHTVOLUMES_ON) || defined(_VRCLIGHTVOLUMES_ADDITIVE_ONLY) || defined(_VRCLIGHTVOLUMESSPECULAR_ON) || defined(_VRCLIGHTVOLUMESSPECULAR_DOMINANT_DIR)
```

ã“ã®ãƒã‚¯ãƒ­ã‚’ä»¥ã¦å¿…è¦ãƒ•ã‚¡ã‚¤ãƒ«ã®ã‚¤ãƒ³ã‚¯ãƒ«ãƒ¼ãƒ‰ã‚’è¡Œã†ï¼
ãƒ‘ãƒƒã‚±ãƒ¼ã‚¸ãƒãƒãƒ¼ã‚¸ãƒ£ã§å°å…¥ã•ã‚Œã¦ã„ã‚‹å‰æã®ãƒ‘ã‚¹ã¨ã—ã¦ã„ã‚‹ãŒï¼Œunitypackageã§å°å…¥ã•ã‚Œã¦ã„ã‚‹å ´åˆã«ã‚‚å¯¾å¿œã—ãŸã„å ´åˆã¯ï¼Œ

```hlsl
#if defined(USE_VRCLIGHTVOLUMES)
#    include "Packages/red.sim.lightvolumes/Shaders/LightVolumes.cginc"
#endif  // defined(USE_VRCLIGHTVOLUMES)
```

`calcLightingCustom()` ã¯ä¸‹è¨˜ã®é€šã‚Šï¼
`ShadeSHPerPixel()` ã‚’ç½®ãæ›ãˆã‚‹å½¢ã§ä½¿ç”¨ã™ã‚‹ï¼

```hlsl
/*!
 * Calculate lighting with custom method.
 * @param [in] color  Base color.
 * @param [in] worldPos  World coordinate.
 * @param [in] worldNormal  Normal in world space.
 * @param [in] atten  Light attenuation.
 * @param [in] ambient  Ambient light.
 * @return Lighting applied color.
 */
half4 calcLightingCustom(half4 color, float3 worldPos, float3 worldNormal, half atten, half3 ambient)
{
    const float3 worldViewDir = normalize(_WorldSpaceCameraPos - worldPos);
#if defined(USING_LIGHT_MULTI_COMPILE) && defined(USING_DIRECTIONAL_LIGHT)
    const float3 worldLightDir = UnityWorldSpaceLightDir(worldPos);
#else
    const float3 worldLightDir = normalize(UnityWorldSpaceLightDir(worldPos));
#endif  // defined(USING_LIGHT_MULTI_COMPILE) && defined(USING_DIRECTIONAL_LIGHT)
    const fixed3 lightCol = _LightColor0.rgb * atten;

    // Lambertian reflectance.
    const float nDotL = dot(worldNormal, worldLightDir);
    const half3 diffuse = lightCol * pow(nDotL * 0.5 + 0.5, 2.0);  // will be mul instruction.

    // Specular reflection.
    const float nDotH = dot(worldNormal, normalize(worldLightDir + worldViewDir));
    const half3 specular = pow(max(0.0, nDotH), _SpecPower) * _SpecColor.rgb * lightCol;

    // Ambient color.
#if UNITY_SHOULD_SAMPLE_SH
#    if defined(USE_VRCLIGHTVOLUMES)
    ambient = calcLightVolumeEmission(color.rgb, worldPos, worldNormal, worldViewDir, 0.0, 0.0);
#    else
    ambient = ShadeSHPerPixel(worldNormal, ambient, worldPos);
#    endif  // defined(USE_VRCLIGHTVOLUMES)
#endif  // UNITY_SHOULD_SAMPLE_SH

    const half4 outColor = half4((diffuse + ambient) * color.rgb + specular, color.a);

    return outColor;
}
```

`calcLightingUnity()` ã¯ä¸‹è¨˜ã®é€šã‚Šï¼
`LightingXXX_GI()` ã¨ `LightingXXX()` ã®é–“ã«VRC Light Volumesã®å‡¦ç†ã‚’å…¥ã‚Œã‚‹ï¼
ã“ã®å‡¦ç†ã‚’é€šã—ãŸéš›ã¯ `UnityGIInput` ã® `indirect.diffuse` ã‚’ã‚¼ãƒ­ãƒ™ã‚¯ãƒˆãƒ«ã«ã™ã‚‹å¿…è¦ãŒã‚ã‚‹ï¼
ã“ã‚ŒãŒ `ShadeSHPerPixel()` ã‚’ç„¡åŠ¹ã«ã™ã‚‹å‡¦ç†ã«è©²å½“ã—ã¦ãŠã‚Šï¼Œã‚µãƒ³ãƒ—ãƒ«ã‚³ãƒ¼ãƒ‰ã®

```hlsl
#pragma surface surf Standard keepalpha fullforwardshadows exclude_path:deferred noambient
```

ã® `noambient` ã«å¯¾å¿œã™ã‚‹å‡¦ç†ã¨ãªã£ã¦ã„ã‚‹ï¼
ï¼ˆ`noambient` ãŒæŒ‡å®šã•ã‚Œã¦ã„ã‚‹å ´åˆï¼Œã‚µãƒ¼ãƒ•ã‚§ãƒ¼ã‚¹ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã®å‡ºåŠ›ã‚³ãƒ¼ãƒ‰ä¸­ã§ `sutf()` é–¢æ•°ã®å‘¼ã³å‡ºã—å…ƒã§ `indirect.diffuse` ã‚’ã‚¼ãƒ­ã¨ã™ã‚‹å‡¦ç†ãŒè¿½åŠ ã•ã‚Œã‚‹ï¼‰

```hlsl
/*!
 * Calculate lighting.
 * @param [in] color  Base color.
 * @param [in] worldPos  World coordinate.
 * @param [in] worldNormal  Normal in world space.
 * @param [in] atten  Light attenuation.
 * @param [in] ambient  Ambient light.
 * @return Lighting applied color.
 */
half4 calcLightingUnity(half4 color, float3 worldPos, float3 worldNormal, half atten, half3 ambient)
{
    // Uniform variable requirements:
    //
    // Variant                             | `_Glossiness` | `_Metallic` | `_SpecColor` | `_SpecPower`
    // ------------------------------------|---------------|-------------|--------------|-------------
    // `_LIGHTING_UNITY_LAMBERT`           |               |             |              |
    // `_LIGHTING_UNITY_BLINN_PHONG`       | o             |             | o            | o
    // `_LIGHTING_UNITY_STANDARD`          | o             | o           |              |
    // `_LIGHTING_UNITY_STANDARD_SPECULAR` | o             |             | o            |

#if defined(_LIGHTING_UNITY_STANDARD)
#    define LightingUnity_GI(so, giInput, gi) LightingStandard_GI(so, giInput, gi)
#    define LightingUnity(so, worldViewDir, gi) LightingStandard(so, worldViewDir, gi)
    SurfaceOutputStandard so;
    UNITY_INITIALIZE_OUTPUT(SurfaceOutputStandard, so);
    so.Albedo = color.rgb;
    so.Normal = worldNormal;
    so.Emission = half3(0.0, 0.0, 0.0);
    so.Metallic = _Metallic;
    so.Smoothness = _Glossiness;
    so.Occlusion = 1.0;
    so.Alpha = color.a;
#elif defined(_LIGHTING_UNITY_STANDARD_SPECULAR)
#    define LightingUnity_GI(so, giInput, gi) LightingStandardSpecular_GI(so, giInput, gi)
#    define LightingUnity(so, worldViewDir, gi) LightingStandardSpecular(so, worldViewDir, gi)
    SurfaceOutputStandardSpecular so;
    UNITY_INITIALIZE_OUTPUT(SurfaceOutputStandardSpecular, so);
    so.Albedo = color.rgb;
    so.Specular = _SpecColor.rgb;
    so.Normal = worldNormal;
    so.Emission = half3(0.0, 0.0, 0.0);
    so.Smoothness = _Glossiness;
    so.Occlusion = 1.0;
    so.Alpha = color.a;
#else
    SurfaceOutput so;
    UNITY_INITIALIZE_OUTPUT(SurfaceOutput, so);
    so.Albedo = color.rgb;
    so.Normal = worldNormal;
    so.Emission = fixed3(0.0, 0.0, 0.0);
#    if defined(_LIGHTING_UNITY_BLINN_PHONG)
#        define LightingUnity_GI(so, giInput, gi) LightingBlinnPhong_GI(so, giInput, gi)
#        define LightingUnity(so, worldViewDir, gi) LightingBlinnPhong(so, worldViewDir, gi)
    so.Specular = _SpecPower / 128.0;
    so.Gloss = _Glossiness;
    // NOTE: _SpecColor is used in UnityBlinnPhongLight() used in LightingBlinnPhong().
#    else
#        define LightingUnity_GI(so, giInput, gi) LightingLambert_GI(so, giInput, gi)
#        define LightingUnity(so, worldViewDir, gi) LightingLambert(so, gi)
#    endif  // defined(_LIGHTING_UNITY_BLINN_PHONG)
    so.Alpha = color.a;
#endif  // defined(_LIGHTING_UNITY_STANDARD)

    UnityGI gi = getGI(worldPos, atten);
    const float3 worldViewDir = normalize(UnityWorldSpaceViewDir(worldPos));
#if defined(UNITY_PASS_FORWARDBASE)
    const float4 lmap = float4(0.0, 0.0, 0.0, 0.0);
    UnityGIInput giInput = getGIInput(gi.light, worldPos, worldNormal, worldViewDir, atten, lmap, ambient);
    LightingUnity_GI(so, giInput, /* inout */ gi);
#endif  // defined(UNITY_PASS_FORWARDBASE)

#if UNITY_SHOULD_SAMPLE_SH && !defined(LIGHTMAP_ON)
#    if defined(USE_VRCLIGHTVOLUMES)
    if (_UdonLightVolumeEnabled && _UdonLightVolumeCount != 0) {
#    if defined(_LIGHTING_UNITY_STANDARD) || defined(_LIGHTING_UNITY_STANDARD_SPECULAR) || defined(_LIGHTING_UNITY_BLINN_PHONG)
        const half glossiness = _Glossiness;
#    else
        const half glossiness = 0.0;
#    endif  // defined(_LIGHTING_UNITY_STANDARD) || defined(_LIGHTING_UNITY_STANDARD_SPECULAR) || defined(_LIGHTING_UNITY_BLINN_PHONG)
#    if defined(_LIGHTING_UNITY_STANDARD)
        const half metallic = _Metallic;
#    else
        const half metallic = 0.0;
#    endif  // defined(_LIGHTING_UNITY_STANDARD)
        gi.indirect.diffuse = half3(0.0, 0.0, 0.0);
        so.Emission += calcLightVolumeEmission(color.rgb, worldPos, worldNormal, worldViewDir, glossiness, metallic);
    }
#    endif  // defined(USE_VRCLIGHTVOLUMES)
#endif  // UNITY_SHOULD_SAMPLE_SH && !defined(LIGHTMAP_ON)

    half4 col = LightingUnity(so, worldViewDir, gi);
#if defined(UNITY_PASS_FORWARDBASE)
    col.rgb += so.Emission;
#endif  // defined(UNITY_PASS_FORWARDBASE)

    return col;

#undef LightingUnity_GI
#undef LightingUnity
}
```

### LTCGI

ã‚µãƒ³ãƒ—ãƒ«ã‚³ãƒ¼ãƒ‰: [Assets/koturn/SphereTracingTips/04_LightingSpecial/Shaders/VRCLVAndLTCGI.shader](https://github.com/koturn/SphereTracingTips/blob/main/Assets/koturn/SphereTracingTips/04_LightingSpecial/Shaders/VRCLVAndLTCGI.shader "Assets/koturn/SphereTracingTips/04_LightingSpecial/Shaders/VRCLVAndLTCGI.shader")

[LTCGI](https://github.com/PiMaker/ltcgi/blob/v1.6.3/Shaders/LTCGI_Simple.shader "PiMaker/ltcgi")ã«å¯¾å¿œã™ã‚‹ã«ã¯[å…¬å¼ã®ã‚µãƒ³ãƒ—ãƒ«ã‚³ãƒ¼ãƒ‰](https://github.com/PiMaker/ltcgi/blob/v1.6.3/Shaders/LTCGI_Simple.shader "PiMaker/ltcgi Shaders/LTCGI_Simple.shader")ã‚’çœŸä¼¼ã¦ï¼Œä¸‹è¨˜ã®å¯¾å¿œã‚’è¡Œã†ã¨ã‚ˆã„ï¼

1. Tagsã« `"LTCGI" = "_LTCGI"` ã®ã‚ˆã†ã«ï¼Œæœ‰åŠ¹ã‹ç„¡åŠ¹ã‹ã‚’æ ¼ç´ã™ã‚‹floatå‹ã®uniformå¤‰æ•°åã‚’æŒ‡å®šï¼ˆã“ã®ä¾‹ã¯ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã« `uniform float _LTCGI` ã‚’å®£è¨€ã™ã‚‹ä¾‹ï¼‰<br>
   ã‚ã‚‹ã„ã¯ï¼Œ`"LTCGI" = "ALWAYS"` ã¨æŒ‡å®šã—ï¼Œå¸¸ã«å®Ÿè¡Œæ™‚ã«åˆ¤å®šã‚’è¡Œã‚ãªã„ã“ã¨ã‚’æ˜ç¤ºï¼<br>
   ã©ã¡ã‚‰ã‚’ç”¨ã„ã‚‹ã‹ã®åˆ¤æ–­åŸºæº–ã¯ä¸‹è¨˜ã®é€šã‚Šï¼
    - `"LTCGI" = "å¤‰æ•°å"`: LTCGIã®å‡¦ç†ã‚³ãƒ¼ãƒ‰ã‚’ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã«å«ã‚ï¼Œå®Ÿè¡Œæ™‚åˆ¤å®šã‚’è¡Œã„ãŸã„å ´åˆï¼
    - `"LTCGI" = "ALWAYS"`: LTCGIã®å‡¦ç†ã‚³ãƒ¼ãƒ‰è‡ªä½“ã‚’ã‚­ãƒ¼ãƒ¯ãƒ¼ãƒ‰æŒ‡å®š (`#pragma shader_feature_local_fragment`) ã§å«ã‚ãªã„ã‚ˆã†ã«ã—ãŸã„å ´åˆï¼
2. `LTCGI_Contribution()` ã‚’ç”¨ã„ã¦diffuseã¨specularã‚’è¨ˆç®—ï¼
3. å‡ºåŠ›ã‚«ãƒ©ãƒ¼ã«åæ˜ ï¼ˆdiffuseã¯ä¹—ç®—ï¼Œspecularã¯åŠ ç®—ï¼‰ï¼

LTCGIã¯v1ã¨v2ã®APIãŒã‚ã‚‹ãŒï¼Œæœ¬è¨˜äº‹ã§ã¯v1ã®APIã‚’åˆ©ç”¨ã™ã‚‹ä¾‹ã‚’ç¤ºã™ï¼

ã¾ãšï¼Œä»Šå›ã¯ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã‚­ãƒ¼ãƒ¯ãƒ¼ãƒ‰ã«ã‚ˆã‚ŠLTCGTIã®æœ‰åŠ¹ãƒ»ç„¡åŠ¹ã‚’åˆ‡ã‚Šæ›¿ãˆã‚‹ã‚ˆã†ã«ã™ã‚‹ï¼
ãƒ—ãƒ­ãƒ‘ãƒ†ã‚£ã¨ã‚­ãƒ¼ãƒ¯ãƒ¼ãƒ‰å®šç¾©ã¯ä¸‹è¨˜ã®ã¨ãŠã‚Šï¼

```shaderlab
[Toggle(_LTCGI_ON)]
_LTCGI ("LTCGI", Int) = 0
```

```hlsl
#pragma shader_feature_local_fragment _ _LTCGI_ON
```

ã“ã®ã‚­ãƒ¼ãƒ¯ãƒ¼ãƒ‰ã‚’ç”¨ã„ã¦ï¼Œã‚¤ãƒ³ã‚¯ãƒ«ãƒ¼ãƒ‰ã‚’ä¸‹è¨˜ã®ã‚ˆã†ã«è¡Œã†ï¼

```hlsl
#if defined(_LTCGI_ON)
#    define LTCGI_AVATAR_MODE
#    if defined(_LIGHTING_UNITY_LAMBERT)
#        define LTCGI_SPECULAR_OFF
#    endif  // defined(_LIGHTING_UNITY_LAMBERT)
#    include "Packages/at.pimaker.ltcgi/Shaders/LTCGI.cginc"
#endif  // defined(_LTCGI_ON)
```

`LTCGI_AVATAR_MODE` ãŒå®šç¾©ã•ã‚Œã¦ã„ã‚‹ã¨ãƒ©ã‚¤ãƒˆãƒãƒƒãƒ—ã®å‚ç…§ã‚’è¡Œã‚ãªã„ãªã©ï¼Œéstaticãªã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆå‘ã‘ã«æœ€é©åŒ–ã§ãã‚‹ï¼
ã¾ãŸï¼Œ `LTCGI_SPECULAR_OFF` ãŒå®šç¾©ã•ã‚Œã¦ã„ã‚‹ã¨ï¼Œã‚¹ãƒšã‚­ãƒ¥ãƒ©ã®è¨ˆç®—å‡¦ç†ã‚’é™¤å¤–ã™ã‚‹ã“ã¨ãŒã§ãã‚‹ï¼

å‰è¿°ã®ã‚ˆã†ã«ã‚¿ã‚°è¨­å®šãŒå¿…è¦ã¨ãªã‚‹ã®ã§ï¼Œä¸‹è¨˜ã®ã‚ˆã†ã«æŒ‡å®šã™ã‚‹ï¼

```shaderlab
Tags
{
    "Queue" = "AlphaTest"
    // "RenderType" = "Transparent"
    "DisableBatching" = "True"
    "IgnoreProjector" = "True"
    "VRCFallback" = "Hidden"
    "LTCGI" = "ALWAYS"
}
```

ã‚ã¨ã¯LTCGIã‚’å‡¦ç†ã«çµ„ã¿è¾¼ã‚€ã ã‘ã§ã‚ã‚‹ï¼

`calcLightingCustom()` ã¯ä¸‹è¨˜ã®é€šã‚Šï¼
`LTCGI_Contribution()` ã‚’ç”¨ã„ã¦ï¼ŒLTCGIã®Diffuseã¨Specularã‚’å–å¾—ã—ï¼Œãã‚Œã‚’ç”¨ã„ãŸçµæœã‚’åŠ ç®—åˆæˆã™ã‚‹ã ã‘ã§ã‚ã‚‹ï¼

```hlsl
/*!
 * Calculate lighting with custom method.
 * @param [in] color  Base color.
 * @param [in] worldPos  World coordinate.
 * @param [in] worldNormal  Normal in world space.
 * @param [in] atten  Light attenuation.
 * @param [in] ambient  Ambient light.
 * @return Lighting applied color.
 */
half4 calcLightingCustom(half4 color, float3 worldPos, float3 worldNormal, half atten, half3 ambient)
{
    const float3 worldViewDir = normalize(_WorldSpaceCameraPos - worldPos);
#if defined(USING_LIGHT_MULTI_COMPILE) && defined(USING_DIRECTIONAL_LIGHT)
    const float3 worldLightDir = UnityWorldSpaceLightDir(worldPos);
#else
    const float3 worldLightDir = normalize(UnityWorldSpaceLightDir(worldPos));
#endif  // defined(USING_LIGHT_MULTI_COMPILE) && defined(USING_DIRECTIONAL_LIGHT)
    const fixed3 lightCol = _LightColor0.rgb * atten;

    // Lambertian reflectance.
    const float nDotL = dot(worldNormal, worldLightDir);
    const half3 diffuse = lightCol * pow(nDotL * 0.5 + 0.5, 2.0);  // will be mul instruction.

    // Specular reflection.
    const float nDotH = dot(worldNormal, normalize(worldLightDir + worldViewDir));
    const half3 specular = pow(max(0.0, nDotH), _SpecPower) * _SpecColor.rgb * lightCol;

    // Ambient color.
#if UNITY_SHOULD_SAMPLE_SH
#    if defined(USE_VRCLIGHTVOLUMES)
    ambient = calcLightVolumeEmission(color.rgb, worldPos, worldNormal, worldViewDir, 0.0, 0.0);
#    else
    ambient = ShadeSHPerPixel(worldNormal, ambient, worldPos);
#    endif  // defined(USE_VRCLIGHTVOLUMES)
#endif  // UNITY_SHOULD_SAMPLE_SH

    half4 outColor = half4((diffuse + ambient) * color.rgb + specular, color.a);

#if defined(_LTCGI_ON)
    float3 ltcgiSpecular = float3(0.0, 0.0, 0.0);
    float3 ltcgiDiffuse = float3(0.0, 0.0, 0.0);
    LTCGI_Contribution(
       worldPos,
       worldNormal,
       worldViewDir,
       1.0 - lossiness,
       float2(0.0, 0.0),
       /* inout */ ltcgiDiffuse,
       /* inout */ ltcgiSpecular);
#        if defined(LTCGI_SPECULAR_OFF)
    outColor.rgb += color.rgb * ltcgiDiffuse;
#        else
    outColor.rgb += color.rgb * ltcgiDiffuse + ltcgiSpecular;
#        endif  // defined(LTCGI_SPECULAR_OFF)
#    endif  // defined(_LTCGI_ON)
#endif  // defined(_LTCGI_ON)

    return outColor;
}
```

`calcLightingUnity()` ã¯ä¸‹è¨˜ã®é€šã‚Šï¼
VRC Light Volumesã®ã‚ˆã†ã«ï¼Œ `ShadeSHPerPixel()` ã‚’ç½®ãæ›ãˆã‚‹ã‚‚ã®ã§ã¯ãªã„ãŸã‚ï¼Œ`gi.indirect.diffuse` ã‚’ã‚¼ãƒ­ã«è¨­å®šã™ã‚‹å‡¦ç†ã¯å¿…è¦ãªãï¼Œå˜ã«è¿½åŠ ã§åŠ ç®—åˆæˆã™ã‚‹è‰²ã¨ã—ã¦æ‰±ã†ã ã‘ã§ã‚ˆã„ï¼

```hlsl
/*!
 * Calculate lighting.
 * @param [in] color  Base color.
 * @param [in] worldPos  World coordinate.
 * @param [in] worldNormal  Normal in world space.
 * @param [in] atten  Light attenuation.
 * @param [in] ambient  Ambient light.
 * @return Lighting applied color.
 */
half4 calcLightingUnity(half4 color, float3 worldPos, float3 worldNormal, half atten, half3 ambient)
{
    // Uniform variable requirements:
    //
    // Variant                             | `_Glossiness` | `_Metallic` | `_SpecColor` | `_SpecPower`
    // ------------------------------------|---------------|-------------|--------------|-------------
    // `_LIGHTING_UNITY_LAMBERT`           |               |             |              |
    // `_LIGHTING_UNITY_BLINN_PHONG`       | o             |             | o            | o
    // `_LIGHTING_UNITY_STANDARD`          | o             | o           |              |
    // `_LIGHTING_UNITY_STANDARD_SPECULAR` | o             |             | o            |

#if defined(_LIGHTING_UNITY_STANDARD)
#    define LightingUnity_GI(so, giInput, gi) LightingStandard_GI(so, giInput, gi)
#    define LightingUnity(so, worldViewDir, gi) LightingStandard(so, worldViewDir, gi)
    SurfaceOutputStandard so;
    UNITY_INITIALIZE_OUTPUT(SurfaceOutputStandard, so);
    so.Albedo = color.rgb;
    so.Normal = worldNormal;
    so.Emission = half3(0.0, 0.0, 0.0);
    so.Metallic = _Metallic;
    so.Smoothness = _Glossiness;
    so.Occlusion = 1.0;
    so.Alpha = color.a;
#elif defined(_LIGHTING_UNITY_STANDARD_SPECULAR)
#    define LightingUnity_GI(so, giInput, gi) LightingStandardSpecular_GI(so, giInput, gi)
#    define LightingUnity(so, worldViewDir, gi) LightingStandardSpecular(so, worldViewDir, gi)
    SurfaceOutputStandardSpecular so;
    UNITY_INITIALIZE_OUTPUT(SurfaceOutputStandardSpecular, so);
    so.Albedo = color.rgb;
    so.Specular = _SpecColor.rgb;
    so.Normal = worldNormal;
    so.Emission = half3(0.0, 0.0, 0.0);
    so.Smoothness = _Glossiness;
    so.Occlusion = 1.0;
    so.Alpha = color.a;
#else
    SurfaceOutput so;
    UNITY_INITIALIZE_OUTPUT(SurfaceOutput, so);
    so.Albedo = color.rgb;
    so.Normal = worldNormal;
    so.Emission = fixed3(0.0, 0.0, 0.0);
#    if defined(_LIGHTING_UNITY_BLINN_PHONG)
#        define LightingUnity_GI(so, giInput, gi) LightingBlinnPhong_GI(so, giInput, gi)
#        define LightingUnity(so, worldViewDir, gi) LightingBlinnPhong(so, worldViewDir, gi)
    so.Specular = _SpecPower / 128.0;
    so.Gloss = _Glossiness;
    // NOTE: _SpecColor is used in UnityBlinnPhongLight() used in LightingBlinnPhong().
#    else
#        define LightingUnity_GI(so, giInput, gi) LightingLambert_GI(so, giInput, gi)
#        define LightingUnity(so, worldViewDir, gi) LightingLambert(so, gi)
#    endif  // defined(_LIGHTING_UNITY_BLINN_PHONG)
    so.Alpha = color.a;
#endif  // defined(_LIGHTING_UNITY_STANDARD)

    UnityGI gi = getGI(worldPos, atten);
    const float3 worldViewDir = normalize(UnityWorldSpaceViewDir(worldPos));
#if defined(UNITY_PASS_FORWARDBASE)
    const float4 lmap = float4(0.0, 0.0, 0.0, 0.0);
    UnityGIInput giInput = getGIInput(gi.light, worldPos, worldNormal, worldViewDir, atten, lmap, ambient);
    LightingUnity_GI(so, giInput, /* inout */ gi);
#endif  // defined(UNITY_PASS_FORWARDBASE)

#if UNITY_SHOULD_SAMPLE_SH && !defined(LIGHTMAP_ON)
#    if defined(USE_VRCLIGHTVOLUMES)
#    if defined(_LIGHTING_UNITY_STANDARD) || defined(_LIGHTING_UNITY_STANDARD_SPECULAR) || defined(_LIGHTING_UNITY_BLINN_PHONG)
        const half glossiness = _Glossiness;
#    else
        const half glossiness = 0.0;
#    endif  // defined(_LIGHTING_UNITY_STANDARD) || defined(_LIGHTING_UNITY_STANDARD_SPECULAR) || defined(_LIGHTING_UNITY_BLINN_PHONG)
    if (_UdonLightVolumeEnabled && _UdonLightVolumeCount != 0) {
#        if defined(_LIGHTING_UNITY_STANDARD)
        const half metallic = _Metallic;
#        else
        const half metallic = 0.0;
#        endif  // defined(_LIGHTING_UNITY_STANDARD)
        gi.indirect.diffuse = half3(0.0, 0.0, 0.0);
        so.Emission += calcLightVolumeEmission(color.rgb, worldPos, worldNormal, worldViewDir, glossiness, metallic);
    }
#    endif  // defined(USE_VRCLIGHTVOLUMES)

#    if defined(_LTCGI_ON)
    float3 ltcgiSpecular = float3(0.0, 0.0, 0.0);
    float3 ltcgiDiffuse = float3(0.0, 0.0, 0.0);
    LTCGI_Contribution(
       worldPos,
       worldNormal,
       worldViewDir,
       1.0 - glossiness,
       float2(0.0, 0.0),
       /* inout */ ltcgiDiffuse,
       /* inout */ ltcgiSpecular);
#        if defined(LTCGI_SPECULAR_OFF)
    so.Emission += color.rgb * ltcgiDiffuse;
#        else
    so.Emission += color.rgb * ltcgiDiffuse + ltcgiSpecular;
#        endif  // defined(LTCGI_SPECULAR_OFF)
#    endif  // defined(_LTCGI_ON)
#endif  // UNITY_SHOULD_SAMPLE_SH && !defined(LIGHTMAP_ON)

    half4 col = LightingUnity(so, worldViewDir, gi);
#if defined(UNITY_PASS_FORWARDBASE)
    col.rgb += so.Emission;
#endif  // defined(UNITY_PASS_FORWARDBASE)

    return col;

#undef LightingUnity_GI
#undef LightingUnity
}
```

---

## ãƒ†ã‚¯ã‚¹ãƒãƒ£ã‚’è²¼ã‚Šä»˜ã‘ã‚‹

ãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ã§ã¯3Dãƒ¢ãƒ‡ãƒ«ãŒé ‚ç‚¹å˜ä½ã§æŒã£ã¦ã„ã‚‹uvåº§æ¨™ã‚’ãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ã®æç”»å¯¾è±¡ã«çµã³ã¤ã‘ãªã„ãŸã‚ï¼Œãƒ†ã‚¯ã‚¹ãƒãƒ£ã‚’æç”»ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã«è²¼ã‚Šä»˜ã‘ã‚‹ã«ã¯ä¸€å·¥å¤«å¿…è¦ã¨ãªã‚‹ï¼

### Tri-Planar

Tri-Planarã¨ã¯xè»¸ï¼Œyè»¸ï¼Œzè»¸ã®ä¸‰æ–¹å‘ã‹ã‚‰ã®å¹³é¢ãƒãƒƒãƒ”ãƒ³ã‚°ã‚’è¡Œã†æ‰‹æ³•ã§ã‚ã‚‹ï¼
åŸºæœ¬å½¢ã¯ä¸‹è¨˜ã®ã‚ˆã†ãªå½¢ã¨ãªã‚‹ï¼

```hlsl
/*!
 * @brief Sample texture using Tri-Planar texture mapping.
 *
 * @param [in] tex  2D texture samplar.
 * @param [in] pos  Object/World space position.
 * @param [in] normal  Normal.
 *
 * @return Sampled color.
 */
half4 tex2DTriPlanar(sampler2D tex, float3 pos, float3 normal)
{
    float3 blending = normalize(max(abs(normal), 0.00001));
    blending /= dot(blending, (1.0).xxx);
    const half4 xaxis = tex2D(tex, pos.yz);
    const half4 yaxis = tex2D(tex, pos.xz);
    const half4 zaxis = tex2D(tex, pos.xy);
    return xaxis * blending.x + yaxis * blending.y + zaxis * blending.z;
}
```

`pos`, `normal` ã¯3æ¬¡å…ƒã®åº§æ¨™ã¨ãã®åº§æ¨™ã«ãŠã‘ã‚‹æ³•ç·šã‚’æ„å‘³ã™ã‚‹ãŒï¼Œãƒ¯ãƒ¼ãƒ«ãƒ‰ç©ºé–“ã®ã‚‚ã®ã‹ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆç©ºé–“ã®ã‚‚ã®ã‚’ç”¨ã„ã‚‹ã‹ã¯ä¸€è€ƒã®ä½™åœ°ãŒã‚ã‚‹ï¼
ãƒ¯ãƒ¼ãƒ«ãƒ‰ç©ºé–“ã§ã®ãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ã§ã‚ã‚Œã°ï¼ŒTri-Planarã®å…¥åŠ›ã¨ã—ã¦ã‚‚ãƒ¯ãƒ¼ãƒ«ãƒ‰ç©ºé–“ã«ãŠã‘ã‚‹åº§æ¨™ã¨æ³•ç·šã‚’ç”¨ã„ã¦ã‚‚å•é¡Œã¯ãªã„ãŒï¼Œç§»å‹•ã‚„å›è»¢ç­‰ã®å¤‰åŒ–ãŒã‚ã‚‹ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆç©ºé–“ã§ã®ãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ã§ã‚ã‚Œã°ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆç©ºé–“ã®åº§æ¨™ã¨æ³•ç·šã‚’ç”¨ã„ã‚‹æ–¹ãŒã‚ˆã„ã¨æ€ã†ï¼

---

## ã‚¢ãƒ«ã‚´ãƒªã‚ºãƒ ã®æ”¹è‰¯

### ãƒ¬ã‚¤ã®é–‹å§‹ç‚¹ï¼Œçµ‚äº†ç‚¹

#### ãƒ¬ã‚¤ã®é–‹å§‹ç‚¹ã‚’ãƒ¡ãƒƒã‚·ãƒ¥è¡¨é¢ã«è¨­å®š

ä¸‹è¨˜ã®ã©ã¡ã‚‰ã‹ã®å ´åˆã«é©ç”¨å¯èƒ½ãªæ‰‹æ³•ã§ã‚ã‚‹ï¼

- æŠ•å½±å…ˆãŒQuadã§Cubeå†…éƒ¨ã«ãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã‚’æç”»ã™ã‚‹å ´åˆ
- æŠ•å½±å…ˆãŒCubeã‹ã¤ `Cull Back` ã®æŒ‡å®šãŒã‚ã‚Šï¼ŒCubeå†…éƒ¨ã«ãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã‚’æç”»ã™ã‚‹å ´åˆ

ç‰¹ã«æç”»å¯¾è±¡ãŒQuadã®å ´åˆã¯çª“ã‹ã‚‰è¦—ãè¾¼ã‚€ã¨å‘ã“ã†å´ã«æ™¯è‰²ãŒè¦‹ãˆã‚‹ã¨ã„ã†å ´åˆã¯ï¼Œé–‹å§‹ç‚¹ã¯ãƒ¡ãƒƒã‚·ãƒ¥ã®è¡¨é¢ã‹ã‚‰ã«ã—ã¦ãŠã„ãŸæ–¹ãŒã‚ˆã„ï¼
modã‚’åˆ©ç”¨ã—ã¦æ™¯è‰²ãŒç„¡é™ã«åºƒãŒã‚‹ã‚ˆã†ãªã‚‚ã®ã‚’æç”»ã™ã‚‹å ´åˆï¼ŒQuadã‚ˆã‚Šæ‰‹å‰ã«æç”»ã•ã‚Œã‚‹ã“ã¨ã§ï¼Œé•å’Œæ„Ÿã®ã‚ã‚‹è¦‹ãˆæ–¹ã«ãªã‚‹ãŸã‚ã ï¼


[![ãƒ¡ãƒƒã‚·ãƒ¥ã®è¡¨é¢ã‚’ãƒ¬ã‚¤ã®é–‹å§‹ç‚¹ã¨ã™ã‚‹](/images/koturn-unity-sphere-tracing-tips/StartFromFrontOfMesh.png)](/images/koturn-unity-sphere-tracing-tips/StartFromFrontOfMesh.png "ãƒ¡ãƒƒã‚·ãƒ¥ã®è¡¨é¢ã‚’ãƒ¬ã‚¤ã®é–‹å§‹ç‚¹ã¨ã™ã‚‹")


#### Cull Front

ãƒãƒªã‚´ãƒ³ã®è£é¢ã«å¯¾ã—ã¦æç”»ã‚’è¡Œã†å ´åˆã¯ãƒ¬ã‚¤ã®é–‹å§‹ç‚¹ã‚’ã‚«ãƒ¡ãƒ©ã§ã¯ãªãï¼Œãƒ¡ãƒƒã‚·ãƒ¥è¡¨é¢ã«è¨­å®šã™ã‚‹ã“ã¨ãŒã§ãã‚‹ï¼

### ãƒ¬ã‚¤é€²è¡Œã‚¢ãƒ«ã‚´ãƒªã‚ºãƒ ã®æ”¹å–„

#### Over-relaxation sphere tracing

2014å¹´ã®ç™ºè¡¨æ‰‹æ³•ï¼
ãƒ¬ã‚¤ã‚’å¤šã‚ã«é€²ã‚ã¦ï¼Œå¢ƒç•Œé¢ã‚’è¶…éã—ãŸå ´åˆã«å¼•ãè¿”ã™ã¨ã„ã†æ‰‹æ³•ï¼

- [è«–æ–‡](https://diglib.eg.org/items/8ea5fa60-fe2f-4fef-8fd0-3783cb3200f0)

ã‚¢ãƒ«ã‚´ãƒªã‚ºãƒ ã®ç–‘ä¼¼ã‚³ãƒ¼ãƒ‰ã¯ä¸‹è¨˜ã®é€šã‚Šï¼
$\boldsymbol{f}$ ã¯è·é›¢é–¢æ•°ï¼Œ $\boldsymbol{s}$ ã¯ãƒ¬ã‚¤ã®å§‹ç‚¹ã‹ã‚‰ã®è·é›¢ï¼ˆé€²è¡Œé•·ï¼‰ã‚’å¼•æ•°ã«å—ã‘å–ã£ã¦ï¼Œãƒ¬ã‚¤ã®ç¾åœ¨ä½ç½®ï¼ˆãƒ™ã‚¯ãƒˆãƒ«ï¼‰ã‚’è¿”ã™é–¢æ•°ã§ã‚ã‚‹ï¼

[![Over-relaxation sphere tracingã®ç–‘ä¼¼ã‚³ãƒ¼ãƒ‰](/images/koturn-unity-sphere-tracing-tips/PseudoCode_OverRelaxation.png)](/images/koturn-unity-sphere-tracing-tips/PseudoCode_OverRelaxation.png "Over-relaxation sphere tracingã®ç–‘ä¼¼ã‚³ãƒ¼ãƒ‰")

ãƒãƒ¼ãƒãƒ³ã‚°ãƒ«ãƒ¼ãƒ—éƒ¨åˆ†ã®å®Ÿè£…ã®ã¿ã‚’æŠœç²‹ã™ã‚‹ã¨ä¸‹è¨˜ã®é€šã‚Šï¼

```hlsl
#if defined(_CALCSPACE_WORLD)
const float3 rayOrigin = _WorldSpaceCameraPos;
#else
const float3 rayOrigin = mul(unity_WorldToObject, float4(_WorldSpaceCameraPos, 1.0)).xyz;
#endif  // defined(_CALCSPACE_WORLD)
const float3 rayDir = normalize(fi.fragPos - rayOrigin);

const float3 rcpScales = rcp(_Scales);
const float marchingFactor = rsqrt(dot(rayDir * rcpScales, rayDir * rcpScales));

float rayLength = 0.0;
float r = asfloat(0x7f800000);  // +inf
float d = 0.0;
for (int i = 0; abs(r) >= _MinRayLength && rayLength < _MaxRayLength && i < _MaxLoop; i++) {
    const float nextRayLength = rayLength + d * marchingFactor;
    const float nextR = map((rayOrigin + rayDir * nextRayLength) * rcpScales);
    if (d <= r + abs(nextR)) {
        d = _OverRelaxFactor * nextR;
        rayLength = nextRayLength;
        r = nextR;
    } else {
        d = r;
    }
}

if (abs(r) >= _MinRayLength) {
    discard;
}
```

ç–‘ä¼¼ã‚³ãƒ¼ãƒ‰ã¨ä¸Šè¨˜ã‚³ãƒ¼ãƒ‰ä¸­ã®é–¢æ•°ã¨å¤‰æ•°ã®å¯¾å¿œé–¢ä¿‚ã¯ä¸‹è¨˜ã®è¡¨ã®ã¨ãŠã‚Šï¼

| ç–‘ä¼¼ã‚³ãƒ¼ãƒ‰ä¸­ã®é–¢æ•°ãƒ»å¤‰æ•° | ã‚³ãƒ¼ãƒ‰ä¸­ã®é–¢æ•°ãƒ»å¤‰æ•°ãƒ»è¨ˆç®— |
|-|-|
| $f$ | `map` |
| $\boldsymbol{s}$ | å¯¾å¿œé–¢æ•°ãªã—ï¼Œ`rayOrigin + rayDir * nextRayLength` ã®è¨ˆç®—ãŒè©²å½“ |
| $\epsilon$ | `_MinRayLength` |
| $t_{max}$ | `_MaxRayLength` |
| $i_{max}$ | `_MaxLoop` |
| $\omega$ | `_RelaxFactor` |
| $t$ | `rayLength` |
| $T$ | `nextRayLength` |
| $z$ | `d` |
| $r$ | `r` |
| $R$ | `nextR` |

#### Accelerating Sphere Tracing

ã‚µãƒ³ãƒ—ãƒ«ã‚³ãƒ¼ãƒ‰: [Assets/koturn/SphereTracingTips/06_Algorithm/Shaders/Accelerating.shader](https://github.com/koturn/SphereTracingTips/blob/main/Assets/koturn/SphereTracingTips/06_Algorithm/Shaders/Accelerating.shader "Assets/koturn/SphereTracingTips/06_Algorithm/Shaders/Accelerating.shader")

2019/02 ã®ç™ºè¡¨æ‰‹æ³•ï¼
å‰æ‰‹æ³•ã¨æ¯”è¼ƒã—ã¦ï¼Œå¢ƒç•Œé¢ã®è¶…éãŒç™ºç”Ÿã—ã«ãã„ã‚ˆã†ã«æ”¹è‰¯ã—ãŸæ‰‹æ³•ã¨ã„ãˆã‚‹ï¼

- [ãƒã‚¹ã‚¿ãƒ¼](https://www.researchgate.net/publication/331547302_Accelerating_Sphere_Tracing "Accelerating Sphere Tracing")
- [è«–æ–‡](https://www.researchgate.net/publication/329152815_Accelerating_Sphere_Tracing "Accelerating Sphere Tracing")

ã‚¢ãƒ«ã‚´ãƒªã‚ºãƒ ã®ç–‘ä¼¼ã‚³ãƒ¼ãƒ‰ã¯ä¸‹è¨˜ã®é€šã‚Šï¼

[![Accelerating Sphere Tracingã®ç–‘ä¼¼ã‚³ãƒ¼ãƒ‰](/images/koturn-unity-sphere-tracing-tips/PseudoCode_Accelerating.png)](/images/koturn-unity-sphere-tracing-tips/PseudoCode_Accelerating.png "Accelerating Sphere Tracingã®ç–‘ä¼¼ã‚³ãƒ¼ãƒ‰")

ãƒãƒ¼ãƒãƒ³ã‚°ãƒ«ãƒ¼ãƒ—éƒ¨åˆ†ã®å®Ÿè£…ã®ã¿ã‚’æŠœç²‹ã™ã‚‹ã¨ä¸‹è¨˜ã®é€šã‚Šï¼

```hlsl
#if defined(_CALCSPACE_WORLD)
const float3 rayOrigin = _WorldSpaceCameraPos;
#else
const float3 rayOrigin = mul(unity_WorldToObject, float4(_WorldSpaceCameraPos, 1.0)).xyz;
#endif  // defined(_CALCSPACE_WORLD)
const float3 rayDir = normalize(fi.fragPos - rayOrigin);

const float3 rcpScales = rcp(_Scales);
const float marchingFactor = rsqrt(dot(rayDir * rcpScales, rayDir * rcpScales));

float rayLength = 0.0;
float r = map((rayOrigin + rayDir * rayLength) * rcpScales);
float d = r;
for (int i = 1; r >= _MinRayLength && (rayLength + r * marchingFactor) < _MaxRayLength && i < _MaxLoop; i++) {
    const float nextRayLength = rayLength + d * marchingFactor;
    const float nextR = map((rayOrigin + rayDir * nextRayLength) * rcpScales);
    if (d <= r + abs(nextR)) {
        const float deltaR = nextR - r;
        const float2 zr = d.xx + deltaR * float2(1.0, -1.0);
        d = nextR + _AccelarationFactor * nextR * (zr.x / zr.y);
        rayLength = nextRayLength;
        r = nextR;
    } else {
        d = r;
    }
}

if (abs(r) >= _MinRayLength) {
    discard;
}
```

ç–‘ä¼¼ã‚³ãƒ¼ãƒ‰ã¨ä¸Šè¨˜ã‚³ãƒ¼ãƒ‰ä¸­ã®é–¢æ•°ã¨å¤‰æ•°ã®å¯¾å¿œé–¢ä¿‚ã¯ä¸‹è¨˜ã®è¡¨ã®ã¨ãŠã‚Šï¼

| ç–‘ä¼¼ã‚³ãƒ¼ãƒ‰ä¸­ã®é–¢æ•°ãƒ»å¤‰æ•° | ã‚³ãƒ¼ãƒ‰ä¸­ã®é–¢æ•°ãƒ»å¤‰æ•°ãƒ»è¨ˆç®— |
|-|-|
| $f$ | `map` |
| $\boldsymbol{s}$ | å¯¾å¿œé–¢æ•°ãªã—ï¼Œ`rayOrigin + rayDir * nextRayLength` ã®è¨ˆç®—ãŒè©²å½“ |
| $\epsilon$ | `_MinRayLength` |
| $t_{max}$ | `_MaxRayLength` |
| $i_{max}$ | `_MaxLoop` |
| $\omega$ | `_AccelarationFactor` |
| $t$ | `rayLength` |
| $T$ | `nextRayLength` |
| $z$ | `d` |
| $r$ | `r` |
| $R$ | `nextR` |

ç–‘ä¼¼ã‚³ãƒ¼ãƒ‰ä¸­ã® $\dfrac{T - t + R - r}{T - t - (R - r)}$ ã®åˆ†æ¯ã¨åˆ†å­ã‚’æ±‚ã‚ã‚‹éš›ã«è¨ˆç®—ã®ãƒ™ã‚¯ãƒˆãƒ«åŒ–ãŒå¯èƒ½ãªãŸã‚ï¼Œä¸‹è¨˜ã®ã‚ˆã†ã«è¨ˆç®—ã—ã¦ã„ã‚‹ï¼

```hlsl
const float deltaR = nextR - r;
const float2 zr = d.xx + deltaR * float2(1.0, -1.0);
```

#### Automatic Step Size Relaxation in Sphere Tracing

ã‚µãƒ³ãƒ—ãƒ«ã‚³ãƒ¼ãƒ‰: [Assets/koturn/SphereTracingTips/06_Algorithm/Shaders/AutomaticStepSizeRelaxation.shader](https://github.com/koturn/SphereTracingTips/blob/main/Assets/koturn/SphereTracingTips/06_Algorithm/Shaders/AutomaticStepSizeRelaxation.shader "Assets/koturn/SphereTracingTips/06_Algorithm/Shaders/AutomaticStepSizeRelaxation.shader")

2023/05 ã®ç™ºè¡¨æ‰‹æ³•ï¼

è«–æ–‡ä¸­ã«ã¯å‰è¿°ã®2æ‰‹æ³•ã®ç–‘ä¼¼ã‚³ãƒ¼ãƒ‰ã®è¨˜è¼‰ã‚‚ã‚ã‚‹ãŸã‚ï¼Œå˜ç´”ã«å®Ÿè£…ã®å‚è€ƒã«ã™ã‚‹ã«ã¯ã“ã®è«–æ–‡ã®ã¿å‚ç…§ã™ã‚‹ã ã‘ã§ã‚‚å•é¡Œãªã„ï¼

- [è«–æ–‡](https://www.researchgate.net/publication/370902411_Automatic_Step_Size_Relaxation_in_Sphere_Tracing "Automatic Step Size Relaxation in Sphere Tracing")

ã‚¢ãƒ«ã‚´ãƒªã‚ºãƒ ã®ç–‘ä¼¼ã‚³ãƒ¼ãƒ‰ã¯ä¸‹è¨˜ã®é€šã‚Šï¼

[![Automatic Step Size Relaxation in Sphere Tracingã®ç–‘ä¼¼ã‚³ãƒ¼ãƒ‰](/images/koturn-unity-sphere-tracing-tips/PseudoCode_AutomaticStepSizeRelaxation.png)](/images/koturn-unity-sphere-tracing-tips/PseudoCode_AutomaticStepSizeRelaxation.png "Automatic Step Size Relaxation in Sphere Tracingã®ç–‘ä¼¼ã‚³ãƒ¼ãƒ‰")

ãƒãƒ¼ãƒãƒ³ã‚°ãƒ«ãƒ¼ãƒ—éƒ¨åˆ†ã®å®Ÿè£…ã®ã¿ã‚’æŠœç²‹ã™ã‚‹ã¨ä¸‹è¨˜ã®é€šã‚Šï¼

```hlsl
#if defined(_CALCSPACE_WORLD)
const float3 rayOrigin = _WorldSpaceCameraPos;
#else
const float3 rayOrigin = mul(unity_WorldToObject, float4(_WorldSpaceCameraPos, 1.0)).xyz;
#endif  // defined(_CALCSPACE_WORLD)
const float3 rayDir = normalize(fi.fragPos - rayOrigin);

const float3 rcpScales = rcp(_Scales);
const float marchingFactor = rsqrt(dot(rayDir * rcpScales, rayDir * rcpScales));

float rayLength = 0.0;
float r = map((rayOrigin + rayDir * rayLength) * rcpScales);
float d = r;
float m = -1.0;

for (int i = 1; r >= _MinRayLength && (rayLength + r * marchingFactor) < _MaxRayLength && i < _MaxLoop; i++) {
    const float nextRayLength = rayLength + d * marchingFactor;
    const float nextR = map((rayOrigin + rayDir * nextRayLength) * rcpScales);
    if (d <= r + abs(nextR)) {
        m = lerp(m, (nextR - r) / d, _AutoRelaxFactor);
        rayLength = nextRayLength;
        r = nextR;
    } else {
        m = -1.0;
    }
    d = 2.0 * r / (1.0 - m);
}

if (r >= _MinRayLength) {
    discard;
}
```

ç–‘ä¼¼ã‚³ãƒ¼ãƒ‰ã¨ä¸Šè¨˜ã‚³ãƒ¼ãƒ‰ä¸­ã®é–¢æ•°ã¨å¤‰æ•°ã®å¯¾å¿œé–¢ä¿‚ã¯ä¸‹è¨˜ã®è¡¨ã®ã¨ãŠã‚Šï¼

| ç–‘ä¼¼ã‚³ãƒ¼ãƒ‰ä¸­ã®é–¢æ•°ãƒ»å¤‰æ•° | ã‚³ãƒ¼ãƒ‰ä¸­ã®é–¢æ•°ãƒ»å¤‰æ•°ãƒ»è¨ˆç®— |
|-|-|
| $f$ | `map` |
| $\boldsymbol{s}$ | å¯¾å¿œé–¢æ•°ãªã—ï¼Œ`rayOrigin + rayDir * nextRayLength` ã®è¨ˆç®—ãŒè©²å½“ |
| $\epsilon$ | `_MinRayLength` |
| $t_{max}$ | `_MaxRayLength` |
| $i_{max}$ | `_MaxLoop` |
| $\beta$ | `_AutoRelaxFactor` |
| $t$ | `rayLength` |
| $T$ | `nextRayLength` |
| $z$ | `d` |
| $r$ | `r` |
| $R$ | `nextR` |
| $m$ | `m` |
| $M$ | `(nextR - r) / d` |

---

## Conservative Depth Output

ã‚µãƒ³ãƒ—ãƒ«ã‚³ãƒ¼ãƒ‰: [Assets/koturn/SphereTracingTips/07_ConservationDepthOutput/Shaders/ConservationDepthOutput.shader](https://github.com/koturn/SphereTracingTips/blob/main/Assets/koturn/SphereTracingTips/07_ConservationDepthOutput/Shaders/ConservationDepthOutput.shader "Assets/koturn/SphereTracingTips/07_ConservationDepthOutput/Shaders/ConservationDepthOutput.shader")

`SV_Depth` ã®å‡ºåŠ›ã‚’æœ‰åŠ¹ã«ã—ãŸå ´åˆï¼ŒEarly-ZãŒè¡Œã‚ã‚Œãªããªã‚‹ï¼
ãã®ãŸã‚ï¼Œä¾‹ãˆã°æç”»å¯¾è±¡ã®ãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆãŒCubeã«å†…åŒ…ã•ã‚Œã‚‹ã‚‚ã®ã§ã‚ã‚Šï¼Œä»–ã®ä¸é€æ˜ãƒ¡ãƒƒã‚·ãƒ¥ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã«ã‚ˆã‚Šé®è”½ã•ã‚Œã‚‹å ´åˆã§ã‚ã£ã¦ã‚‚æç”»å‡¦ç†ã¯è¡Œã‚ã‚Œã¦ã—ã¾ã†ï¼

[![ãƒãƒªã‚´ãƒ³ãƒ¡ãƒƒã‚·ãƒ¥ã«ã‚ˆã‚Šãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆãŒé®è”½ã•ã‚Œã‚‹ä¾‹](/images/koturn-unity-sphere-tracing-tips/OcclusionByPolygonMesh.png )](/images/koturn-unity-sphere-tracing-tips/OcclusionByPolygonMesh.png "ãƒãƒªã‚´ãƒ³ãƒ¡ãƒƒã‚·ãƒ¥ã«ã‚ˆã‚Šãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆãŒé®è”½ã•ã‚Œã‚‹ä¾‹")

`SV_Depth` ã®ä»£ã‚ã‚Šã« `SV_DepthLessEqual`, `SV_DepthGreaterEqual` ã¨ã„ã†ã‚»ãƒãƒ³ãƒ†ã‚£ã‚¯ã‚¹ã‚’ç”¨ã„ã‚Œã°ï¼ŒæŠ•å½±å…ˆãƒ¡ãƒƒã‚·ãƒ¥ã‚ˆã‚Šå¾Œæ–¹ï¼Œã‚ã‚‹ã„ã¯å‰æ–¹ã¸ã®æ·±åº¦å‡ºåŠ›ã‚’è¡Œã‚ãªã„ä»®å®šã‚’ä¸ãˆã‚‹ã“ã¨ãŒã§ãï¼ŒæŠ•å½±é¢ã«å¯¾ã—ã¦Early-Zã‚’è¡Œã†ã“ã¨ãŒå¯èƒ½ã¨ãªã‚‹ï¼

| ã‚»ãƒãƒ³ãƒ†ã‚£ã‚¯ã‚¹ | èª¬æ˜ |
|-|-|
| `SV_DepthLessEqual` | æŠ•å½±å…ˆãƒ¡ãƒƒã‚·ãƒ¥ã‚ˆã‚Šå¾Œã‚å´ã«ãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã‚’æç”»ã™ã‚‹éš›ã«ç”¨ã„ã‚‹ï¼Quadã¸ã®æŠ•å½±ã‚„èƒŒé¢ã‚«ãƒªãƒ³ã‚° (`Cull Back`) ã‚’æœ‰åŠ¹ã«ã—ã¦ã„ã‚‹Cubeã¸ã®æŠ•å½±ã®å ´åˆãªã©ï¼ |
| `SV_DepthGreaterEqual` | æŠ•å½±å…ˆãƒ¡ãƒƒã‚·ãƒ¥ã‚ˆã‚Šå‰å´ã«ãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã‚’æç”»ã™ã‚‹éš›ã«ç”¨ã„ã‚‹ï¼å‰é¢ã‚«ãƒªãƒ³ã‚° (`Cull Front`) ã‚’æœ‰åŠ¹ã«ã—ã¦ã„ã‚‹Cubeã¸ã®æŠ•å½±ã®å ´åˆãªã©ï¼ |

ã‚µãƒ³ãƒ—ãƒ«ã‚³ãƒ¼ãƒ‰ã§ã¯å„ã‚»ãƒãƒ³ãƒ†ã‚£ã‚¯ã‚¹ã‚’ã‚­ãƒ¼ãƒ¯ãƒ¼ãƒ‰ã«ã‚ˆã‚‹åˆ‡ã‚Šæ›¿ãˆå¯èƒ½ã¨ã™ã‚‹ãŸã‚ã«ï¼Œãƒ™ãƒ¼ã‚¹ã¨ã™ã‚‹ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã®ãƒˆã‚°ãƒ«å¼ã®ãƒ—ãƒ­ãƒ‘ãƒ†ã‚£ã‹ã‚‰ `KeywordEnum` ã«ã‚ˆã‚‹é¸æŠå¼ã¸å¤‰æ›´ã—ã¦ã„ã‚‹ï¼

```shaderlab
[KeywordEnum(Off, On, LessEqual, GreaterEqual)]
_SVDepth ("SV_Depth ouput", Int) = 1
```

```hlsl
#pragma shader_feature_local_fragment _SVDEPTH_OFF _SVDEPTH_ON _SVDEPTH_LESSEQUAL _SVDEPTH_GREATEREQUAL
```

`SV_DepthLessEqual` ã¾ãŸã¯ `SV_DepthGreaterEqual` ã‚’ä½¿ç”¨ã™ã‚‹ãŸã‚ã«ã¯ `#pragma target 5.0` ã®æŒ‡å®šãŒå¿…è¦ã¨ãªã‚‹ï¼
Unity 2021.2 ã‹ã‚‰ã¯ç‰¹å®šã®ã‚­ãƒ¼ãƒ¯ãƒ¼ãƒ‰ãŒæœ‰åŠ¹æ™‚ã«ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ãƒ¢ãƒ‡ãƒ«ã®å¤‰æ›´ãŒå¯èƒ½ã¨ãªã£ã¦ã„ã‚‹ã®ã§ï¼Œä¸å¿…è¦ã« `#pragma target 5.0` ã‚’ä½¿ç”¨ã—ãŸããªã„ã®ã§ã‚ã‚Œã°ä¸‹è¨˜ã®ã‚ˆã†ã«æŒ‡å®šã™ã‚‹ã¨ã‚ˆã„ï¼

```hlsl
#if UNITY_VERSION >= 202030
#    pragma target 3.0
#    pragma target 5.0 _SVDEPTH_LESSEQUAL _SVDEPTH_GREATEREQUAL
#else
#    pragma target 5.0
#endif  // UNITY_VERSION >= 202030
```

ã‚»ãƒãƒ³ãƒ†ã‚£ã‚¯ã‚¹ã®å¸åã¯ä¸‹è¨˜ã®ãƒã‚¯ãƒ­å®šç¾©ã§è¡Œã£ã¦ã„ã‚‹ï¼

```hlsl
#if defined(_SVDEPTH_ON)
#    define DEPTH_SEMANTICS SV_Depth
#elif defined(_SVDEPTH_LESSEQUAL)
#    define DEPTH_SEMANTICS SV_DepthLessEqual
#elif defined(_SVDEPTH_GREATEREQUAL)
#    define DEPTH_SEMANTICS SV_DepthGreaterEqual
#endif  // defined(_SVDEPTH_ON)
```

ä»–ãƒ˜ãƒƒãƒ€ãƒ•ã‚¡ã‚¤ãƒ«ã¸ã®åˆ†é›¢ã‚’è€ƒãˆã¦ã„ã‚‹ãªã‚‰ï¼Œä¸‹è¨˜ã®ã‚ˆã†ã«ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ãƒ¢ãƒ‡ãƒ«ãŒéå¯¾å¿œã§ã‚ã‚‹ã¨ãã« `SV_DepthLessEqual` ã¾ãŸã¯ `SV_DepthGreaterEqual` ã‚’ `SV_Depth` ã¸ãƒ•ã‚©ãƒ¼ãƒ«ãƒãƒƒã‚¯ã™ã‚‹ã®ã‚‚ã‚¢ãƒªã ã¨æ€ã†ï¼

```hlsl
#if defined(_SVDEPTH_ON)
#    define DEPTH_SEMANTICS SV_Depth
#elif defined(_SVDEPTH_LESSEQUAL)
#    if SHADER_TARGET >= 45
#        define DEPTH_SEMANTICS SV_DepthLessEqual
#    else
#        define DEPTH_SEMANTICS SV_Depth
#    endif  // SHADER_TARGET >= 45
#elif defined(_SVDEPTH_GREATEREQUAL)
#    if SHADER_TARGET >= 45
#        define DEPTH_SEMANTICS SV_DepthGreaterEqual
#    else
#        define DEPTH_SEMANTICS SV_Depth
#    endif  // SHADER_TARGET >= 45
#endif  // defined(_SVDEPTH_ON)
```


åŒæ™‚ã«ãƒ™ãƒ¼ã‚¹ã¨ã™ã‚‹ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã§ `_SVDEPTH_ON` ã®å®šç¾©æœ‰ç„¡ã§åˆ¤å®šã—ã¦ã„ãŸç®‡æ‰€ã¯ `DEPTH_SEMANTICS` ã®å®šç¾©æœ‰ç„¡ã§ã®åˆ¤å®šã«å·®ã—æ›¿ãˆãŸï¼


### Cull Backã®Cubeã§ã®å‡ºåŠ›çµæœ

ãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã¨Quadã‚’äº¤å·®ã•ã›ãŸå ´åˆã®æç”»çµæœã®å…·ä½“ä¾‹ã‚’ç¤ºã™ï¼

æŠ•å½±é¢ã‚ˆã‚Šå¾Œã‚å´ã«ãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ãƒ–ã‚¸ã‚§ã‚¯ãƒˆã‚’æç”»ã™ã‚‹ã«ã‚‚é–¢ã‚ã‚‰ãš `SV_DepthGreaterEqual` ã‚’æŒ‡å®šã—ã¦ã„ã‚‹å ´åˆï¼ŒQuadã¨ã®äº¤å·®ã—ã¦ã„ã‚‹ã‚ãŸã‚Šã§æ­£å¸¸ã«æç”»ãŒè¡Œã‚ã‚Œãªããªã£ã¦ã„ã‚‹ï¼

| `SV_Depth` | `SV_DepthLessEqual` | `SV_DepthGreaterEqual` |
|-|-|-|
| [![Cull Backã®Cubeã¸ã®æŠ•å½±ã¨Quadã¨ã®äº¤å·® SV_Depth](/images/koturn-unity-sphere-tracing-tips/CubeCullBackSVDepth.png)](/images/koturn-unity-sphere-tracing-tips/CubeCullBackSVDepth.png "Cull Backã®Cubeã¸ã®æŠ•å½±ã¨Quadã¨ã®äº¤å·®") | [![Cull Backã®Cubeã¸ã®æŠ•å½±ã¨Quadã¨ã®äº¤å·® SV_DepthLessEqual](/images/koturn-unity-sphere-tracing-tips/CubeCullBackSVDepthLessEqual.png)](/images/koturn-unity-sphere-tracing-tips/CubeCullBackSVDepthLessEqual.png "Cull Backã®Cubeã¸ã®æŠ•å½±ã¨Quadã¨ã®äº¤å·® SV_DepthLessEqual") | [![Cull Backã®Cubeã¸ã®æŠ•å½±ã¨Quadã¨ã®äº¤å·® SV_DepthGreaterEqual](/images/koturn-unity-sphere-tracing-tips/CubeCullBackSVDepthGreaterEqual.png )](/images/koturn-unity-sphere-tracing-tips/CubeCullBackSVDepthGreaterEqual.png "Cull Backã®Cubeã¸ã®æŠ•å½±ã¨Quadã¨ã®äº¤å·® SV_DepthGreaterEqual") |

### Cull Frontã®Cubeã§ã®å‡ºåŠ›çµæœ

æŠ•å½±é¢ãŒQuadã§é®è”½ã•ã‚Œã¦ã„ã‚‹å ´åˆï¼Œ `SV_DepthLessEqual` ãŒæŒ‡å®šã•ã‚Œã¦ã„ã‚‹ã¨å…ˆã«æŠ•å½±é¢ã®Z-TestãŒè¡Œã‚ã‚Œã¦ã—ã¾ã†ãŸã‚ï¼Œæç”»ãŒæ­£å¸¸ã«è¡Œã‚ã‚Œãªã£ã¦ã„ã‚‹ï¼
å…¨é¢çš„ã«æç”»ã•ã‚Œãªããªã‚‹ã®ã§ã¯ãªãï¼Œéƒ¨åˆ†çš„ã«æç”»ãŒæ¬ ã‘ã‚‹ç¾è±¡ã¨ãªã£ã¦ã„ã‚‹ï¼

| `SV_Depth` | `SV_DepthLessEqual` | `SV_DepthGreaterEqual` |
|-|-|-|
| [![Cull Frontã®Cubeã¸ã®æŠ•å½±ã¨Quadã¨ã®äº¤å·® SV_Depth](/images/koturn-unity-sphere-tracing-tips/CubeCullFrontSVDepth.png)](/images/koturn-unity-sphere-tracing-tips/CubeCullFrontSVDepth.png "Cull Frontã®Cubeã¸ã®æŠ•å½±ã¨Quadã¨ã®äº¤å·®") | [![Cull Frontã®Cubeã¸ã®æŠ•å½±ã¨Quadã¨ã®äº¤å·® SV_DepthLessEqual](/images/koturn-unity-sphere-tracing-tips/CubeCullFrontSVDepthLessEqual.png)](/images/koturn-unity-sphere-tracing-tips/CubeCullFrontSVDepthLessEqual.png "Cull Frontã®Cubeã¸ã®æŠ•å½±ã¨Quadã¨ã®äº¤å·® SV_DepthLessEqual") | [![Cull Frontã®Cubeã¸ã®æŠ•å½±ã¨Quadã¨ã®äº¤å·® SV_DepthGreaterEqual](/images/koturn-unity-sphere-tracing-tips/CubeCullFrontSVDepthGreaterEqual.png )](/images/koturn-unity-sphere-tracing-tips/CubeCullFrontSVDepthGreaterEqual.png "Cull Frontã®Cubeã¸ã®æŠ•å½±ã¨Quadã¨ã®äº¤å·® SV_DepthGreaterEqual") |

---

## ãã®ä»–

### ãƒ«ãƒ¼ãƒ—ã®ã‚¢ãƒ³ãƒ­ãƒ¼ãƒ«

[VRChatã§ã©ã†ã—ã¦ã‚‚ ãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ã‚’ã—ãŸã„ã‚ãªãŸã¸è´ˆã‚‹æœ¬ã€Ver 1.1ã€‘](https://butadiene.booth.pm/items/1829988 "VRChatã§ã©ã†ã—ã¦ã‚‚ ãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ã‚’ã—ãŸã„ã‚ãªãŸã¸è´ˆã‚‹æœ¬ã€Ver 1.1ã€‘")ã§è¨˜è¼‰ã•ã‚Œã¦ã„ã‚‹ã‚³ãƒ¼ãƒ‰ã¯ãƒãƒ¼ãƒãƒ³ã‚°ãƒ«ãƒ¼ãƒ—ã«å¯¾ã—ã¦ `[unroll]` ã®æŒ‡å®šãŒã‚ã‚‹ï¼
ã—ã‹ã— `[unroll]` ã¯ã‚³ãƒ³ãƒ‘ã‚¤ãƒ«æ™‚é–“ã¨å‡ºåŠ›ã‚³ãƒ¼ãƒ‰ã‚µã‚¤ã‚ºã‚’è‚¥å¤§åŒ–ã•ã›ã‚‹ãŸã‚ï¼Œä½¿ç”¨ã™ã‚‹ã«ã‚ãŸã£ã¦ã¯æ…é‡ã«æ¤œè¨ã™ã‚‹æ–¹ãŒã‚ˆã„ï¼

ã‚ã¾ã‚Šã«å·¨å¤§ãªã‚³ãƒ¼ãƒ‰ã‚µã‚¤ã‚ºã®ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã«ãªã‚‹ã¨ï¼Œå®Ÿè¡Œæ™‚ã®èª­ã¿è¾¼ã¿ã«æ™‚é–“ã‚’è¦ã™ã‚‹ã“ã¨ã«ãªã‚‹ã‹ã‚‚ã—ã‚Œãªã„ï¼
ã¾ãŸï¼Œã‚³ãƒ³ãƒ‘ã‚¤ãƒ«ã«1åˆ†ã‚‚è¦ã™ã‚‹ã¨ãªã‚‹ã¨ï¼Œé–‹ç™ºã«ãŠã„ã¦ã‚‚ãƒˆãƒ©ã‚¤ã‚¢ãƒ«ï¼†ã‚¨ãƒ©ãƒ¼ã‚’ç¹°ã‚Šè¿”ã—ã«ãããªã‚‹ï¼

---

## å‚è€ƒæ–‡çŒ®

- [VRChatã§ã©ã†ã—ã¦ã‚‚ ãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ã‚’ã—ãŸã„ã‚ãªãŸã¸è´ˆã‚‹æœ¬ã€Ver 1.1ã€‘](https://butadiene.booth.pm/items/1829988 "VRChatã§ã©ã†ã—ã¦ã‚‚ ãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ã‚’ã—ãŸã„ã‚ãªãŸã¸è´ˆã‚‹æœ¬ã€Ver 1.1ã€‘ - Butadiene Works - BOOTH")
- [Unity ã§ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã‚¹ãƒšãƒ¼ã‚¹ã® Raymarching ã‚’ãƒ•ã‚©ãƒ¯ãƒ¼ãƒ‰ãƒ¬ãƒ³ãƒ€ãƒªãƒ³ã‚°ã§ã‚„ã£ã¦ã¿ãŸ - å‡¹ã¿Tips](https://tips.hecomi.com/entry/2018/12/31/211448 "Unity ã§ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã‚¹ãƒšãƒ¼ã‚¹ã® Raymarching ã‚’ãƒ•ã‚©ãƒ¯ãƒ¼ãƒ‰ãƒ¬ãƒ³ãƒ€ãƒªãƒ³ã‚°ã§ã‚„ã£ã¦ã¿ãŸ - å‡¹ã¿Tips")
- [Inigo Quilez :: computer graphics, mathematics, shaders, fractals, demoscene and more](https://iquilezles.org/articles/normalsSDF/ "Inigo Quilez :: computer graphics, mathematics, shaders, fractals, demoscene and more")
- [Unity ã§è·é›¢é–¢æ•°ã®è¨˜è¿°ã ã‘ã§ãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ãŒã§ãã‚‹ uRaymarching ã‚’ Forward / XR å¯¾å¿œã—ãŸ - å‡¹ã¿Tips](https://tips.hecomi.com/entry/2019/01/27/233137 "Unity ã§è·é›¢é–¢æ•°ã®è¨˜è¿°ã ã‘ã§ãƒ¬ã‚¤ãƒãƒ¼ãƒãƒ³ã‚°ãŒã§ãã‚‹ uRaymarching ã‚’ Forward / XR å¯¾å¿œã—ãŸ - å‡¹ã¿Tips")
- [Unity - Manual: Targeting shader models and GPU features in HLSL](https://docs.unity3d.com/2022.3/Documentation/Manual/SL-ShaderCompileTargets.html "Unity - Manual: Targeting shader models and GPU features in HLSL")
- [sqrt (sm4 - asm) - Win32 apps | Microsoft Learn](https://learn.microsoft.com/en-us/windows/win32/direct3dhlsl/sqrt--sm4---asm- "sqrt (sm4 - asm) - Win32 apps | Microsoft Learn")
- [div (sm4 - asm) - Win32 apps | Microsoft Learn](https://learn.microsoft.com/en-us/windows/win32/direct3dhlsl/div--sm4---asm- "div (sm4 - asm) - Win32 apps | Microsoft Learn")
- [rsq (sm4 - asm) - Win32 apps | Microsoft Learn](https://learn.microsoft.com/en-us/windows/win32/direct3dhlsl/rsq--sm4---asm- "rsq (sm4 - asm) - Win32 apps | Microsoft Learn")
- [rsqrt - Win32 apps | Microsoft Learn](https://learn.microsoft.com/en-us/windows/win32/direct3dhlsl/dx-graphics-hlsl-rsqrt "rsqrt - Win32 apps | Microsoft Learn")
- [Semantics - Win32 apps | Microsoft Learn](https://learn.microsoft.com/en-us/windows/win32/direct3dhlsl/dx-graphics-hlsl-semantics "Semantics - Win32 apps | Microsoft Learn")
- [é«˜é€Ÿã«é€†å¹³æ–¹æ ¹ã‚’è¨ˆç®—ã™ã‚‹ã‚¢ãƒ«ã‚´ãƒªã‚ºãƒ ](https://lipoyang.hatenablog.com/entry/2021/02/06/194619 "é«˜é€Ÿé€†å¹³æ–¹æ ¹ (fast inverse square root) ã®ã‚¢ãƒ«ã‚´ãƒªã‚ºãƒ è§£èª¬ - æ»´äº†åºµæ—¥éŒ²")
- [hecomi/uRaymarching](https://github.com/hecomi/uRaymarching "hecomi/uRaymarching")
- [lilxyzw/Shader-MEMO Assets/SPSITest.shader](https://github.com/lilxyzw/Shader-MEMO/blob/main/Assets/SPSITest.shader "Shader-MEMO/Assets/SPSITest.shader at main Â· lilxyzw/Shader-MEMO")
- [pema99/shader-knowledge raymarching.md ](https://github.com/pema99/shader-knowledge/blob/main/raymarching.md "shader-knowledge/raymarching.md at main Â· pema99/shader-knowledge")
- [REDSIM/VRCLightVolumes](https://github.com/REDSIM/VRCLightVolumes "REDSIM/VRCLightVolumes: Nextgen voxel based light probes replacement for VRChat")
- [PiMaker/ltcgi](https://github.com/PiMaker/ltcgi "PiMaker/ltcgi: Optimized plug-and-play realtime area lighting using the linearly transformed cosine algorithm for Unity/VRChat.")
- [Amplified Shader Editor](https://assetstore.unity.com/packages/tools/visual-scripting/amplify-shader-editor-68570 "Amplify Shader Editor | Visual Scripting | Unity Asset Store")
- [koturn/2626d9e33d3fc2b0ea2237c5afe6d8a3](https://gist.github.com/koturn/2626d9e33d3fc2b0ea2237c5afe6d8a3 "https://gist.github.com/koturn/2626d9e33d3fc2b0ea2237c5afe6d8a3")
- [Introduction to Shaders for VRChatter](https://docs.google.com/presentation/d/1BMLSOJMTqasoe8CeDAXFBDUbPzKQVRJ6CSup-rJcnRU/edit#slide=id.p "Introduction to Shaders for VRChatter")
- [2Dãƒ†ã‚¯ã‚¹ãƒãƒ£ã‚’UVã‚’ä½¿ã‚ãšã«æŠ•å½±ã™ã‚‹Tri-Planar Texture Mapping #WebGL - Qiita](https://qiita.com/edo_m18/items/c8995fe91778895c875e "2Dãƒ†ã‚¯ã‚¹ãƒãƒ£ã‚’UVã‚’ä½¿ã‚ãšã«æŠ•å½±ã™ã‚‹Tri-Planar Texture Mapping #WebGL - Qiita")
- [Triplanar Mapping - Unityã‚’é ‘å¼µã‚‹blog](https://ssr-maguro.hatenablog.com/entry/2020/01/22/192000 "Triplanar Mapping - Unityã‚’é ‘å¼µã‚‹blog")
- [Enhanced Sphere Tracing](https://diglib.eg.org/items/8ea5fa60-fe2f-4fef-8fd0-3783cb3200f0 "Enhanced Sphere Tracing")
- [(PDF) Accelerating Sphere Tracing](https://www.researchgate.net/publication/331547302_Accelerating_Sphere_Tracing "Accelerating Sphere Tracing")
- [(PDF) Accelerating Sphere Tracing](https://www.researchgate.net/publication/329152815_Accelerating_Sphere_Tracing "Accelerating Sphere Tracing")
- [(PDF) Automatic Step Size Relaxation in Sphere Tracing](https://www.researchgate.net/publication/370902411_Automatic_Step_Size_Relaxation_in_Sphere_Tracing "Automatic Step Size Relaxation in Sphere Tracing")
- [2023-09-02 ãƒ¬ã‚¤ãƒˆãƒ¬åˆå®¿9 redflash[4D]](https://docs.google.com/presentation/d/1f05HU58XD2w_71CJOdiEqOsBI8L2TYRTMndNT9MPqpI/edit?slide=id.gbd0ef54b81_0_79#slide=id.gbd0ef54b81_0_79 "2023-09-02 ãƒ¬ã‚¤ãƒˆãƒ¬åˆå®¿9 redflash[4D]")
- [Direct3D 11.3 Functional Specification](https://microsoft.github.io/DirectX-Specs/d3d/archive/D3D11_3_FunctionalSpec.htm "Direct3D 11.3 Functional Specification")
- [D3D11è©³è§£ æ–°ã—ã„DirectXãŒã‚‚ãŸã‚‰ã™ã‚¤ãƒãƒ™ãƒ¼ã‚·ãƒ§ãƒ³ã¨ã¯ï¼Ÿ](https://cedec.cesa.or.jp/2009/ssn_archive/pdf/sep1st/SS03.pdf "D3D11è©³è§£ æ–°ã—ã„DirectXãŒã‚‚ãŸã‚‰ã™ã‚¤ãƒãƒ™ãƒ¼ã‚·ãƒ§ãƒ³ã¨ã¯ï¼Ÿ")
- [ãƒ‡ãƒ¢ã‚·ãƒ¼ãƒ³ã¸ã‚ˆã†ã“ã 4KBã§æ˜ åƒä½œå“ã‚’ä½œã‚‹æŠ€è¡“ã€ãŠã‚ˆã³ã‚²ãƒ¼ãƒ é–‹ç™ºã¸ã®å¿œç”¨](http://ftp7.jp.netbsd.org/pub/osdn.net/libraryzip/68289/C17_184.pdf "ãƒ‡ãƒ¢ã‚·ãƒ¼ãƒ³ã¸ã‚ˆã†ã“ã 4KBã§æ˜ åƒä½œå“ã‚’ä½œã‚‹æŠ€è¡“ã€ãŠã‚ˆã³ã‚²ãƒ¼ãƒ é–‹ç™ºã¸ã®å¿œç”¨")
